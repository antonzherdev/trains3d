package core.chain

trait CNIterator<T> {
   def hasNext : bool
   def next : T
}

trait CNBuilder<T, C extends CNTraversable<T>> {
    def add(item : T)
    def build : C
    def addAll(item : CNTraversable<T>) {
        item.for(add(_))
    }
}


trait CNTraversable<T> {
    def for(each : T -> void) {
        go(item -> {
            each(item)
            true
        })
    }

    def go(on : T -> bool) : bool

    def chain : CNChain<T> = {
        CNChain.chainWith<T>(self)
    }

    def find(where : T -> bool) : T? = {
        var ret : T? = nil
        go(x -> {
            if(where(ret)) {
                ret = x
                false
            }
            true
        })
        ret
    }

    def head : T?

    def convertWith<C extends CNTraversable<T>>(builder : CNBuilder<T, C>) : C = {
        for(x -> builder.add(x))
        builder.build
    }
}

trait CNMutableTraversable<T> extends CNTraversable<T> {
    def add(item : T)
    def remove(item : T)
}

trait CNIterable<T> extends CNTraversable<T> {
    def count : uint = {
        val i = iterator
        var n : uint = 0
        while(i.hasNext) {
            i.next
            n++
        }
        n
    }
    def iterator : CNIterator<T>

    def head : T? = if(iterator.hasNext) iterator.next else nil
    def isEmpty : bool = !iterator.hasNext

    def chain : CNChain<T> = {
        CNChain.chainWith<T>(self)
    }
    def for(each : T -> void) {
        val i = iterator
        while(i.hasNext) each(i.next)
    }
    def go(on : T -> bool) : bool = {
        val i = iterator
        while(i.hasNext) if(!on(i.next)) return false
        true
    }

    def contains(item : T) : bool = {
        val i = iterator
        while(i.hasNext) if(i.next == i) return true
        false
    }

    def description : string = {
        chain.toStringWith(start = "[", delimiter = ", ", end = "]")
    }

    def hash : uint = {
        var ret : uint = 13
        val i = iterator
        while(i.hasNext) {
            ret = ret*31 + i.next.hash
        }
        ret
    }
}

trait CNMutableIterable<T> extends CNIterable<T> with CNMutableTraversable<T> {}