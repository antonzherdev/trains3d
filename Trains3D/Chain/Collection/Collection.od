package core.chain

trait Iterator<T> {
   def hasNext : bool
   def next : T
}

trait Builder<T, C extends Traversable<T>> {
    def add(item : T)
    def build : C
    def addAll(item : Traversable<T>) {
        item.for(add(_))
    }
}


trait Traversable<T> {
    def for(each : T -> void) {
        go(item -> {
            each(item)
            true
        })
    }

    def go(on : T -> bool) : bool

    def chain : Chain<T> = {
        Chain.chainWith<T>(self)
    }

    def find(where : T -> bool) : T? = {
        var ret : T? = nil
        go(x -> {
            if(where(ret)) {
                ret = x
                false
            }
            true
        })
        ret
    }

    def head : T?

    def convertWith<C extends Traversable<T>>(builder : Builder<T, C>) : C = {
        for(x -> builder.add(x))
        builder.build
    }
}

trait MutableTraversable<T> extends Traversable<T> {
    def add(item : T)
    def remove(item : T)
}

trait Iterable<T> extends Traversable<T> {
    def count : uint = {
        val i = iterator
        var n : uint = 0
        while(i.hasNext) {
            i.next
            n++
        }
        n
    }
    def iterator : Iterator<T>

    def head : T? = if(iterator.hasNext) iterator.next else nil
    def isEmpty : bool = !iterator.hasNext

    def chain : Chain<T> = {
        Chain.chainWith<T>(self)
    }
    def for(each : T -> void) {
        val i = iterator
        while(i.hasNext) each(i.next)
    }
    def go(on : T -> bool) : bool = {
        val i = iterator
        while(i.hasNext) if(!on(i.next)) return false
        true
    }

    def contains(item : T) : bool = {
        val i = iterator
        while(i.hasNext) if(i.next == i) return true
        false
    }

    def description : string = {
        chain.toStringWith(start = "[", delimiter = ", ", end = "]")
    }

    def hash : uint = {
        var ret : uint = 13
        val i = iterator
        while(i.hasNext) {
            ret = ret*31 + i.next.hash
        }
        ret
    }
}

trait MutableIterable<T> extends Iterable<T> with MutableTraversable<T> {}

class IterableF<T>(iteratorF : () -> Iterator<T>) extends Iterable<T> {
    def iterator = iteratorF()
}

class EmptyIterator<T> extends Iterator<T> {
    static val instance = EmptyIterator<any>()
    def hasNext : bool = false
    def next : T = throw "Iterator is empty"
}