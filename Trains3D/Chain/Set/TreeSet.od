package core.chain

class MutableTreeSet<T>(map : MutableTreeMap<T, Object>) extends MutableSet<T> {
    static def newWith<T>(comparator : (T, T) -> int) : MutableTreeSet<T> = {
        MutableTreeSet<T>(MutableTreeMap<T, Object>(comparator))
    }

    static def apply<T> = MutableTreeSet<T>(MutableTreeMap<T, Object>())
    private static val obj = NSObject()

    def between(a : T, b : T) : [T]

    def append(item : T) {
        map.set(value = obj, forKey = item)
    }

    def remove(item : T) : bool = map.removeFor(key = item).isDefined


    def higherThan(item : T) : T? = map.higherKeyThan(item)
    def lowerThan(item : T) : T? = map.lowerKeyThan(item)

    def count : uint = map.count
    def iterator = map.keys.iterator
    def iteratorHigherThan(item : T) = map.keys.iteratorHigherThan(item)

    def head : T? = map.firstKey
    def last : T? = map.lastKey
    def contains(item : T) : bool = map.contains(key = item)

    def clear {
        map.clear
    }

    def addAll(objects : Traversable<T>) {
        objects.for(append(item = _))
    }

    def reorder : MutableTreeSet<T> = {
        val ret = MutableTreeSet<T>(MutableTreeMap<T, Object>(map.comparator))
        ret.addAll(self)
        ret
    }
}