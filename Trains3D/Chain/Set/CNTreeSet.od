package core.chain

import CNTreeMap
import CNCollection
import CNSet
import CNSeq
import CNChain
import CNOption
import "NSObject+CNOption.h"

class CNMutableTreeSet<T>(map : CNMutableTreeMap<T, ODObject>) extends CNMutableSet<T> {
    static def newWith<T>(comparator : (T, T) -> int) : CNMutableTreeSet<T> = {
        CNMutableTreeSet<T>(CNMutableTreeMap<T, ODObject>(comparator))
    }

    static def new<T> = CNMutableTreeSet<T>(CNMutableTreeMap.new<T, ODObject>)
    private static val obj = NSObject.new

    def between(a : T, b : T) : [T]

    def add(object : T) {
        map.set(object = obj, forKey = object)
    }

    def remove(object : T) : bool = map.removeFor(key = object).isDefined


    def higherThan(object : T) : T? = map.higherKeyThan(object)
    def lowerThan(object : T) : T? = map.lowerKeyThan(object)

    def count : uint = map.count
    def iterator = map.keys.iterator
    def iteratorHigherThan(object : T) = map.keys.iteratorHigherThan(object)

    def head : T? = map.firstKey
    def last : T? = map.lastKey
    def contains(object : T) : bool = map.contains(key = object)

    def clear {
        map.clear
    }

    def addAll(objects : CNTraversable<T>) {
        objects.for(add(object = _))
    }

    def reorder : CNMutableTreeSet<T> = {
        val ret = CNMutableTreeSet<T>(CNMutableTreeMap<T, ODObject>(map.comparator))
        ret.addAll(self)
        ret
    }
}