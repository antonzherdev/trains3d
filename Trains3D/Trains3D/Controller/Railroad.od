package com.antonzherdev.trains

import com.antonzherdev.eg._

abstract class RailroadConnectorContent {
    def canAdd(rail : Rail) : bool = true
    def connect(rail : Rail, to : RailConnector) : RailroadConnectorContent
    def disconnect(rail : Rail, to : RailConnector) : RailroadConnectorContent
    def checkLightIn(connector : RailConnector, mustBe : bool) : RailroadConnectorContent = self
    def rails : [Rail]
    def isGreen : bool = true
    def isEmpty = false
    def cutDownTreesIn(forest : Forest) {}
}

case class EmptyConnector extends RailroadConnectorContent {
    static val instance : RailroadConnectorContent = EmptyConnector()
    override def rails : [Rail] = []
    override def connect(rail : Rail, to : RailConnector) : RailroadConnectorContent = rail
    override def disconnect(rail : Rail, to : RailConnector) : RailroadConnectorContent = self
    override def isEmpty = true
}

case class Rail(val tile : vec2i, val form : RailForm) extends RailroadConnectorContent {
    def has(connector : RailConnector) : bool = form.start == connector || form.end == connector
    override def connect(rail : Rail, to : RailConnector) : RailroadConnectorContent = SwitchState(Switch(rail.tile, to, self, rail), true)
    override def disconnect(rail : Rail, to : RailConnector) : RailroadConnectorContent = EmptyConnector.instance
    override def rails : [Rail] = [self]
    override def checkLightIn(connector : RailConnector, mustBe : bool) : RailroadConnectorContent =
        if(mustBe) RailLightState(RailLight(tile, connector, self), isGreen = true)
        else self
    override def canAdd(rail : Rail) : bool = rail.form != form
    def line : Line2 = form.line + tile
}

case class Switch(val tile : vec2i, val connector : RailConnector, val rail1 : Rail, val rail2 : Rail)  {
    def rails : [Rail] = [rail1, rail2]
    def railPoint1 = railPoint(rail1)
    def railPoint2 = railPoint(rail2)
    def disconnect(rail : Rail) : RailroadConnectorContent = if(rail == rail1) rail2 else rail1
    private def railPoint(rail : Rail) =  RailPoint(tile, rail.form, 0, rail.form.end == connector)
}
case class SwitchState(val switch : Switch, val firstActive : bool) extends RailroadConnectorContent {
    def activeRail : Rail = if(firstActive) switch.rail1 else switch.rail2
    override def rails : [Rail] = if(firstActive) switch.rails else [switch.rail2, switch.rail1]
    override def connect(rail : Rail, to : RailConnector) : RailroadConnectorContent = self
    override def disconnect(rail : Rail, to : RailConnector) : RailroadConnectorContent = switch.disconnect(rail)
    override def cutDownTreesIn(forest : Forest) {
        forest.cutDownFor(switch)
    }
    override def canAdd(rail : Rail) = false
    def turn = SwitchState(switch, !firstActive)
    def connector = switch.connector
    def tile = switch.tile
}

case class RailLight(val tile : vec2i, val connector : RailConnector, val rail : Rail)
case class RailLightState(val light : RailLight, override val isGreen : bool) extends RailroadConnectorContent {
    override def checkLightIn(connector : RailConnector, mustBe : bool) : RailroadConnectorContent = if(mustBe) self else light.rail
    override def rails : [Rail] = [light.rail]
    override def cutDownTreesIn(forest : Forest) {
        forest.cutDownFor(light)
    }
    override def canAdd(rail : Rail) : bool = light.rail.canAdd(rail)
    override def connect(rail : Rail, to : RailConnector) : RailroadConnectorContent = SwitchState(Switch(light.tile, to, light.rail, rail), true)
    override def disconnect(rail : Rail, to : RailConnector) : RailroadConnectorContent = EmptyConnector.instance
    def turn = RailLightState(light, !isGreen)
    def connector = light.connector
    def tile = light.tile

    def shift = vec3(if(light.connector == RailConnector.top) -0.2 else 0.2, 0, -0.45)
}


enum ObstacleType {
    damage()
    switch()
    light()
    end()
}
case class Obstacle(val obstacleType : ObstacleType, val point : RailPoint) {}

class Railroad(val map : MapSso, val score : Score, val forest : Forest) extends Actor {
    private val _connectorIndex = MMapDefault(MHashMap<uint4, RailroadConnectorContent>(), EmptyConnector.instance)
    private var _state : RailroadState = RailroadState(
        id = 0,
        connectorIndex = ImMapDefault(ImHashMap<uint4, RailroadConnectorContent>(), EmptyConnector.instance),
        damages = RailroadDamages([]))
    def state : Future<RailroadState> = prompt {
        _state
    }
    val stateWasRestored = Signal<void>()
    def restore(state : RailroadState) = future {
        if(_state != state) {
            _state = state
            _connectorIndex.map.assign(_state.connectorIndex.map)
            stateWasRestored.post
        }
    }

    def tryAdd(rail : Rail) : Future<bool> = tryAdd(rail, free = false)

    def tryAdd(rail : Rail, free : bool) : Future<bool> = future {
        if(_state.canAdd(rail)) {
            add(rail)
            if(!free) score.railBuilt
            true
        } else false
    }

    val switchWasTurned = Signal<SwitchState>()
    def turn(switch : Switch) : Future<void> = future {
        _state.switches.find(_.switch == switch).for{state ->
            val ns = state.turn
            _connectorIndex.set(key = indexKey(switch.tile, switch.connector), value = ns)
            commitState
            switchWasTurned.post(ns)
        }
    }

    static inline def indexKey(tile : vec2i, connector : RailConnector) : uint4 = (tile.x + 8192)*65536 + (tile.y + 8192)*4 + connector.ordinal

    private def commitState {
        _state = RailroadState(_state.id + 1, _connectorIndex.imCopy, _state.damages)
    }

    val lightWasTurned = Signal<RailLightState>()
    def turn(light : RailLight) : Future<void> = future {
        _state.lights.find(_.light == light).for{state ->
            val ns = state.turn
            _connectorIndex.set(key = indexKey(light.tile, light.connector), value = ns)
            commitState
            lightWasTurned.post(ns)
        }
    }

    val railWasBuilt = Signal<void>()
    private def add(rail : Rail) {
        connect(rail, rail.form.start).cutDownTreesIn(forest)
        connect(rail, rail.form.end).cutDownTreesIn(forest)
        checkLightsNear(rail)
        forest.cutDownFor(rail)
        commitState
        railWasBuilt.post
    }

    val railWasRemoved = Signal<void>()
    def remove(rail : Rail) = future {
        if(_state.rails.contains(rail)) {
            disconnect(rail, rail.form.start)
            disconnect(rail, rail.form.end)
            checkLightsNear(rail)
            commitState
            railWasRemoved.post
        }
    }

    private def connect(rail : Rail, to : RailConnector) =
        _connectorIndex.modify(key = indexKey(rail.tile, to), by = _.connect(rail, to))

    private def disconnect(rail : Rail, to : RailConnector) =
        _connectorIndex.modify( key = indexKey(rail.tile, to), by = _.disconnect(rail, to))

    private def checkLightsNear(rail : Rail) {
        val tile = rail.tile
        checkLightsNear(tile, rail.form.start)
        checkLightsNear(tile, rail.form.end)
    }

    private def checkLightsNear(tile : vec2i, connector : RailConnector) = future {
        if(checkLightsNear(tile, connector, 4, this = true)) commitState
    }

    private def checkLightsNear(tile : vec2i, connector : RailConnector, distance : int, this : bool) : bool = {
        var changed = false
        if(distance <= 0) {
            changed = checkLightIn(tile, connector)
        } else {
            val c = _connectorIndex[indexKey(tile, connector)]
            c.rails.for {rail ->
                val oc = rail.form.otherConnector(than = connector)
                changed = (checkLightsNear(oc.next(tile),
                    connector = oc.otherSideConnector,
                    distance = distance - 1, this = false) || changed)
                changed = (checkLightIn(tile, oc) || changed)
            }
            if(!this) changed = (checkLightIn(tile, connector) || changed)
        }
        changed
    }

    private def needLightsIn(tile : vec2i, connector : RailConnector, distance : int, this : bool) : bool = {
        val content = _connectorIndex[indexKey(tile, connector)]
        if(content.is<RailLightState> && !this) false
        else if(distance == 0) true
        else {
            val nextTile = connector.next(tile)
            val otherSideConnector = connector.otherSideConnector
            val nc = _connectorIndex[indexKey(nextTile, otherSideConnector)]
            nc.rails.exists{ rail ->
                needLightsIn(
                    tile = nextTile,
                    connector = rail.form.otherConnector(than = otherSideConnector),
                    distance = distance - 1, this = false)
            }
        }
    }

    private def needLightsInOtherDirection(tile : vec2i, connector : RailConnector, distance : int, this : bool) : bool = {
        val content = _connectorIndex[indexKey(tile, connector)]
        if(content.is<RailLightState> && !this) false
        else if(distance == 0) true
        else {
            content.rails.exists{ rail ->
                val c = rail.form.otherConnector(than = connector)
                needLightsInOtherDirection(
                    tile = c.next(tile),
                    connector = c.otherSideConnector,
                    distance = distance - 1, this = false)
            }
        }
    }


    private def isTurnRailIn(tile : vec2i, connector : RailConnector) : bool = {
        val rails = _connectorIndex[indexKey(tile, connector)].rails
        rails.count == 1 && rails[0].form.isTurn
    }

    private def checkLightIn(tile : vec2i, connector : RailConnector) : bool = {
        if(needLightsIn(tile, connector, distance = 2, this = true) && needLightsInOtherDirection(tile, connector, distance = 2, this = true)) {
            buildLightIn(tile, connector, true)
        } else if(map.isPartial(tile) && map.isFull(connector.next(tile))) {
            buildLightIn(tile, connector, true)
        } else {
            val nextTile = connector.next(tile)
            val otherSideConnector = connector.otherSideConnector
            val c = _connectorIndex[indexKey(nextTile, otherSideConnector)]
            if(c.is<Rail> && c.rails.exists{ rail ->
                val oc = rail.form.otherConnector(than = otherSideConnector)
                rail.form.isTurn && _connectorIndex[indexKey(nextTile, oc)].is<SwitchState>
            }) {
                buildLightIn(tile, connector, true)
            } else {

                buildLightIn(tile, connector, false)
            }
        }
    }

    val lightWasBuiltOrRemoved = Signal<void>()
    private def buildLightIn(tile : vec2i, connector : RailConnector, mustBe : bool) : bool = {
        var changed = false
        _connectorIndex.modify(key = indexKey(tile, connector)) {content ->
            val r = content.checkLightIn(connector, mustBe = mustBe)
            if(r != content) {
                if(r.is<RailLightState>) r.cutDownTreesIn(forest)
                changed = true
            }
            r
        }
        if(changed) lightWasBuiltOrRemoved.post
        changed
    }


    def addDamageAt(point : RailPoint) : Future<RailPoint> = future {
        var p = point
        if(p.back) p = p.invert

        val fl = p.form.length
        if(p.form.isStraight && p.x.between(0.35, 0.65)) p = p.set(x = 0.35)
        else if(p.x.between(0, 0.3)) p = p.set(x = 0.3)
        else if(p.x.between(fl - 0.3, fl)) p = p.set(x = fl - 0.3)

        if(!map.isVisible(p.point)) {
           p = p.set(x = 0)
           if(!map.isVisible(p.point)) {
               p = p.set(x = fl)
           }
        }

        _state = RailroadState(_state.id + 1, _state.connectorIndex, RailroadDamages(_state.damages.points + p))
        p
    }

    def fixDamageAt(point : RailPoint) = future {
        var p = point
        if(p.back) p = point.invert

        _state = RailroadState(_state.id + 1, _state.connectorIndex, RailroadDamages(_state.damages.points - p))
    }

    def isLocked(rail : Rail) = prompt {
        _state.isLocked(rail)
    }
}

case class RailroadDamages(val points : [RailPoint]) {
    lazy val index : [(vec2i, RailForm) : [float]] = points.chain.group(by = (_.tile, _.form), f = _.x).toMap
}

case class RailroadState(val id : uint, val connectorIndex : ImMapDefault<uint4, RailroadConnectorContent>, val damages : RailroadDamages) {
    lazy val rails : [Rail] = connectorIndex.values.chain.flatMap(_.rails).distinct.toArray
    lazy val switches : [SwitchState] = connectorIndex.values.chain.filterCast(SwitchState.type).toArray
    lazy val lights : [RailLightState] = connectorIndex.values.chain.filterCast(RailLightState.type).toArray

    def railsIn(tile : vec2i) : [Rail] = rails.chain.filter(_.tile == tile).toArray

    def canAdd(rail : Rail) : bool =
        connectorIndex[Railroad.indexKey(rail.tile, rail.form.start)].canAdd(rail)
        && connectorIndex[Railroad.indexKey(rail.tile, rail.form.end)].canAdd(rail)


    private def activeRailFor(tile : vec2i, connector : RailConnector) : Rail? =
        connectorIndex[Railroad.indexKey(tile, connector)].rails.head


    def moveWith(obstacleProcessor : Obstacle -> bool, forLength : float, point : RailPoint) : RailPointCorrection = {
        val p = point + forLength

        val correction = p.correct
        val damage = checkDamagesWith(obstacleProcessor, point, correction.point.x)
        if(damage != nil) {
            val x = damage
            return RailPointCorrection(p.set(x = x), correction.error + correction.point.x - x)
        }

        if(correction.error == 0) {
            val switchCheckCorrection = (correction.point + 0.5).correct
            if(switchCheckCorrection.error == 0) return correction

            val scActiveRailOpt = connectorIndex[Railroad.indexKey(p.tile, p.endConnector)].rails.head
            if(scActiveRailOpt == nil) return correction
            if(scActiveRailOpt.get.form != p.form) {
                if(!obstacleProcessor(Obstacle(ObstacleType.switch, correction.point))) {
                    return RailPointCorrection(switchCheckCorrection.point.add(-0.5), switchCheckCorrection.error)
                }
            }
            return correction
        }

        val connector = p.endConnector
        val connectorDesc = connectorIndex[Railroad.indexKey(p.tile, connector)]
        val activeRailOpt = connectorDesc.rails.head
        if(activeRailOpt == nil) return correction
        else if(activeRailOpt.form != p.form) {
            obstacleProcessor(Obstacle(ObstacleType.switch, correction.point))
            return correction
        }

        if(!connectorDesc.isGreen) {
            if(!obstacleProcessor(Obstacle(ObstacleType.light, correction.point))) {
                return correction
            }
        }

        val nextTile = connector.next(p.tile)
        val otherSideConnector = connector.otherSideConnector
        val nextRail = activeRailFor(nextTile, otherSideConnector)

        if(nextRail == nil) {
            obstacleProcessor(Obstacle(ObstacleType.end, correction.point))
            correction
        } else {
            val form = nextRail.form
            moveWith(obstacleProcessor, correction.error, RailPoint(nextTile, form, 0, form.end == otherSideConnector))
        }
    }


    def checkDamagesWith(obstacleProcessor : Obstacle -> bool, from : RailPoint, to : float) : float? = {
        if(damages.points.isEmpty || from.x == to) return nil
        damages.index((from.tile, from.form)).map{ opt ->
            val on : Float -> bool = x : Float -> {
                !obstacleProcessor(Obstacle(ObstacleType.damage, from.set(x = x)))
            }
            val len = from.form.length
            if(from.back) opt.chain.filter(_.between(len - to, len - from.x)).sortDesc.map(len - _).find(on)
            else opt.chain.filter(_.between(from.x, to)).sort.find(on)
        }
    }

    def contentIn(tile : vec2i, connector : RailConnector) : RailroadConnectorContent =
        connectorIndex[Railroad.indexKey(tile, connector)]

    def isLocked(rail : Rail) : bool = {
        !(damages.index((rail.tile, rail.form)).getOr([]).isEmpty)
    }

    def isConnected(a : RailPoint, b : RailPoint) : bool = _isConnected(a, b, MHashSet<uint4>())

    private def _isConnected(a : RailPoint, b : RailPoint, checked : MSet<uint4>) : bool =
        if(a.tile == b.tile && a.form == b.form) true
        else {
            val endConnector = a.endConnector
            val ot = endConnector.next(a.tile)
            val oc = endConnector.otherSideConnector
            val ik = Railroad.indexKey(ot, oc)
            if(checked.contains(ik)) false
            else {
                checked.append(ik)
                connectorIndex[ik].rails.exists{rail ->
                    val na = RailPoint(tile = ot, rail.form, x = 0, back = rail.form.end == oc)
                    _isConnected(na, b, checked)
                }
            }
        }

}