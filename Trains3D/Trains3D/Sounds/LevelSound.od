package com.antonzherdev.trains


class LevelSound(level : Level) extends SoundPlayersCollection([
    TreeSound(level),
    NotificationSoundPlayer(Sound("CrashBack.wav", 0.7), Level.crashNotification),
    NotificationSoundPlayer(Sound("CrashBack.wav", 0.7), Level.knockDownNotification),
    CollisionSound("Crash1", TrainsDynamicWorld.carsCollisionNotification, 0.5, 1.0),
    CollisionSound("GroundCrash1", TrainsDynamicWorld.carAndGroundCollisionNotification, 0.3, 0.7),
    NotificationSoundPlayer(Sound("SporadicDamage.wav", 0.15), Level.sporadicDamageNotification),
    SignalSoundPlayer(Sound("TrainPreparing.wav", 0.2), level.trainIsExpected),
    SignalSoundPlayer(Sound("TrainRun.wav", 0.1), level.trainIsAboutToRun),
    NotificationSoundPlayer(Sound("CityBuild.wav", 0.15), Level.buildCityNotification, level.cities.count > 2),
    NotificationSoundPlayer(Sound("RefuseBuild.wav", 0.2), RailroadBuilder.refuseBuildNotification),
    NotificationSoundPlayer(Sound("RefuseBuild.wav", 0.2), SwitchProcessor.strangeClickNotification),
    SignalSoundPlayer(Sound("Click.wav", 0.3), level.railroad.switchWasTurned),
    SignalSoundPlayer(Sound("Beep.wav", 0.3), level.railroad.lightWasTurned),
    NotificationSoundPlayer(Sound("BuildMode.wav", 0.3), RailroadBuilder.modeNotification),
    NotificationSoundPlayer(Sound("Fix.wav", 0.3), Level.fixDamageNotification),
    NotificationSoundPlayer(Sound.par(limit = 4, "Choo.wav", 0.05), Train.chooNotification)
])

class CollisionSound(name : string, notificationHandle : NotificationHandle<Level, float4>, impulseK : float4, volume : float4) extends SoundPlayer {
    val sound = Sound.par(limit = 5, "$name.wav")
    private var obs : NotificationObserver?
    def start{
        obs = notificationHandle.observe { _, impulse ->
            var imp = impulseK * impulse.abs
            if(imp > 0.1) {
                if(imp > 1.0) imp = 1.0
                sound.playWith(volume = imp*volume)
            }
        }
    }
    def stop{
        obs.for(_.detach)
        obs = nil
    }
}