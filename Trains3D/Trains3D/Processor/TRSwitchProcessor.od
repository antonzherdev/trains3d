package com.antonzherdev.trains

import com.antonzherdev.eg._

class TRSwitchProcessor(level : TRLevel) extends EGMouseProcessor {
    def process(event : EGEvent) : bool = event.leftMouse(self)

    private val index : EGRectIndex<(TRRailConnector, bool)> = EGRectIndex(
        [
            (EGRect(-0.1, 0.2, 0.2, 0.3), (TRRailConnector.top, false)),
            (EGRect(-0.1, 0.2, -0.5, 0.3), (TRRailConnector.bottom, false)),
            (EGRect(-0.5, 0.3, -0.1, 0.2), (TRRailConnector.left, false)),
            (EGRect(0.2, 0.3, -0.1, 0.2), (TRRailConnector.right, false)),

            (EGRect(0.15, 0.1, 0.4, 0.1), (TRRailConnector.top, true)),
            (EGRect(-0.25, 0.1, -0.5, 0.1), (TRRailConnector.bottom, true)),
            (EGRect(-0.5, 0.1, 0.15, 0.1), (TRRailConnector.left, true)),
            (EGRect(0.4, 0.1, -0.25, 0.1), (TRRailConnector.right, true))
        ]
    )
    private var downed : TRRailroadConnectorContent? = nil

    def mouseDown(event : EGEvent) = {
        val location = event.location
        val tile : EGVec2I = location
        val relPoint = location.sub(tile)
        downed = index[relPoint].flatMap(v -> {
            val content = level.railroad.contentIn(tile, connector = v.a)
            if(v.b) content.as<TRLight>
            else content.as<TRSwitch>
        })
        downed.isDefined
    }

    def mouseDrag(event : EGEvent) = downed.isDefined

    def mouseUp(event : EGEvent) =
        if(downed.isDefined) {
            downed.get.as<TRSwitch>.for(level.tryTurn(theSwitch = _ ))
            downed.get.as<TRLight>.for(_.turn)

            true
        } else false
}