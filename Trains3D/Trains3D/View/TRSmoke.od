package com.antonzherdev.trains

import CNData

import EG
import EGGL
import EGTypes
import EGShader
import EGTexture
import EGMatrix
import EGSurface
import EGMesh
import EGVec
import EGBillboard
import EGParticleSystem
import EGMaterial

import TRTrain
import TRRailPoint

class TRSmoke(weak train : TRTrain) extends EGBillboardParticleSystem {
    private val engine : TRCar = train.cars.head.get
    private val tubePos = engine.carType.engineType.get.tubePos
    private static val zSpeed = 0.1
    private static val emitEvery = 0.005
    private var emitTime = 0.0

    def generateParticlesWith(delta : float) {
        emitTime += delta
        while(emitTime > emitEvery) {
            emitTime -= emitEvery
            emitParticle
        }
    }

    static val particleSize : float4 = 0.03
    static val modelQuad = EGQuad(particleSize)
    static val textureQuadrant : EGQuadrant = EGQuad.identity.quadrant
    static val defColor = EGVec4(1, 1, 1, 0.7)
    def generateParticle : TRSmokeParticle =  {
        val fPos = if(train.isBack) engine.tail.point else engine.head.point
        val bPos = if(train.isBack) engine.head.point else engine.tail.point
        val delta = bPos.sub(fPos)
        val tubeXY = fPos.add(delta.set(length = tubePos.x))
        val emitterPos = EGVec3(tubeXY, tubePos.z)

        val p = TRSmokeParticle()
        p.color = defColor
        p.position = EGVec3(emitterPos.x + randomFloatGap(-0.01, 0.01), emitterPos.y + randomFloatGap(-0.01, 0.01), emitterPos.z)
        p.model = modelQuad
        p.uv = textureQuadrant.randomQuad
        val s = EGVec3((if(train.isBack) fPos.sub(bPos) else delta).set(length = train.speedFloat), zSpeed)
        p.speed = EGVec3(-s.x * randomPercents(0.6), -s.y * randomPercents(0.6), s.z * randomPercents(0.6))
        p
    }
}

class TRSmokeParticle extends EGBillboardParticle(4) {
    var speed : EGVec3
    static val dragCoefficient = 1 // air resistance coefficient | a = -dragCoefficient * sqr(speed)

    def update(t : float4, dt : float4) {
        val a = speed.mul(-dragCoefficient)
        speed = speed.add(a.mul(dt))
        position = position.add(speed.mul(dt))
        if(lifeTime > 3) {
            color = EGVec4(1, 1, 1, 2.8 - 0.7*lifeTime)
        }
    }
}


class TRSmokeView extends EGBillboardParticleSystemView(EGSimpleMaterial(EG.textureFor("Smoke.png")), EGBlendFunction.standard)
