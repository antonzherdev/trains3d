import EGGL
import EGModel

import TRRailroad
import TRRailPoint

import "TR3DRail.h"
import "TR3DRailTurn.h"

class TRRailroadView {
    private val railView = TRRailView.new
    private val switchView = TRSwitchView.new
    private val lightView = TRLightView.new
    private val damageView = TRDamageView.new

    def draw(railroad : TRRailroad) {
        railroad.rails.for(railView.draw(_))
        railroad.switches.for(switchView.draw(_))
        railroad.lights.for(lightView.draw(_))
        railroad.builder.rail.for(railView.draw(_))
        railroad.damagesPoints.for(damageView.draw(_))
    }
}

stub val Rail : string
stub val RailTurn : string

class TRRailView {
    def draw(rail : TRRail) {
        glPushMatrix
        egTranslate(rail.tile.x, rail.tile.y, 0.001)

        egColor3(0.2, 0.2, 0.2)

        if(rail.form == TRRailForm.bottomTop) {
            egRotate(90, 1, 0, 0)
            egDrawJasModel(Rail)
        } else if(rail.form == TRRailForm.leftRight) {
            egRotate(90, 0, 0, 1)
            egRotate(90, 1, 0, 0)
            egDrawJasModel(Rail)
        } else {
            if(rail.form.start.x == 0 && rail.form.start.y == 1) egRotate(90, 0, 0, 1)
            else if(rail.form.start.x == -1 && rail.form.end.y == 1) egRotate(180, 0, 0, 1)
            else if(rail.form.start.x == -1 && rail.form.end.y == -1) egRotate(270, 0, 0, 1)
            egRotate(90, 1, 0, 0)
            egDrawJasModel(RailTurn)
        }
        glPopMatrix
    }
}

class TRSwitchView {
    def draw(theSwitch : TRSwitch) {
        val connector = theSwitch.connector
        glPushMatrix
        egTranslate(theSwitch.tile.x, theSwitch.tile.y, 0.01)
        egRotate(connector.angle, 0, 0, 1)

        val rail = theSwitch.activeRail
        val form = rail.form

        egColor3(0.2, 0.8, 0.2)
        if(form.start.x + form.end.x == 0) {
            glBegin(GL_QUADS)
            egVertex2(-0.5, 0.05)
            egVertex2(-0.5, -0.05)
            egVertex2(-0.25, -0.02)
            egVertex2(-0.25, 0.02)
            glEnd
        } else {
            val otherConnector = if(form.start == connector) form.end else form.start
            val x = connector.x
            val y = connector.y
            val ox = otherConnector.x
            val oy = otherConnector.y
            if(x == -1 && oy == -1 || y == 1 && ox == -1 || y == -1 && ox == 1 || x == 1 && oy == 1) {
                egScale(1, -1, 1)
            }
            glBegin(GL_QUADS)
            egVertex2(-0.5, 0.05)
            egVertex2(-0.5, -0.05)
            egVertex2(-0.25, 0.1)
            egVertex2(-0.25, 0.15)
            glEnd
        }

        glPopMatrix
    }
}

class TRLightView {
    def draw(light : TRLight) {
        glPushMatrix
        egTranslate(light.tile.x, light.tile.y, 0)
        egRotate(light.connector.angle, 0, 0, 1)

        egTranslate(-0.45, 0.2, 0)
        if(light.isGreen) egColor3(0, 1, 0) else egColor3(1, 0, 0)
        glutSolidCube(0.1)

        glPopMatrix
    }
}

class TRDamageView {
    def draw(point : TRRailPoint) {
        glPushMatrix
        egTranslate(point.point.x, point.point.y, 0.01)
        egColor4(1.0, 0.0, 0.0, 0.5)
        egRect(-0.1, -0.1, 0.1, 0.1)
        glPopMatrix
    }
}