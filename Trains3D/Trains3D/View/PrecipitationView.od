package com.antonzherdev.trains

class PrecipitationView extends Controller {
    static def apply(precipitation : Precipitation) : PrecipitationView = {
        if(precipitation.tp == PrecipitationType.rain) RainView(precipitation.strength)
        else throw "Unknown precipitation type"
    }

    def draw
    def updateWith(delta : float)
}

class RainView(strength : float) extends PrecipitationView {
    val system = RainParticleSystem(strength)
    val view = RainSystemView(system)

    def updateWith(delta : float) {
        system.updateWith(delta)
    }

    def draw {
        view.draw
    }
}

class RainParticleSystem(strength : float) extends ParticleSystem<RainParticle> {
    val particles = 0.to(100*strength).chain.map(RainParticle()).toArray
}

class RainParticle extends Particle {
    private var position : vec2 = vec2.rnd*2

    def writeTo(array : VoidRefArray) : VoidRefArray =
        array.write(RainData, RainData(position))
            .write(RainData, RainData(position + vec2(0, 0.1)))

    def updateWith(delta : float) {
    }
}

struct RainData(position : vec2)


class RainSystemView(system : RainParticleSystem) extends ParticleSystemView<RainParticle, RainData>(
    system, RainSystemView.vbDesc, system.particles.count,
    RainShader.instance, nil, BlendFunction.standard)
{
    static val vbDesc = VertexBufferDesc<RainData>(RainData.type, 0, -1, -1, -1, -1)

    def vertexCount : uint = 2
    def indexCount : uint = 2
    def index(vertexCount : uint, maxCount : uint) : IndexSource = EmptyIndexSource.lines
}

class RainShaderText extends ShaderTextBuilder {
    def vertex =
        "$vertexHeader
         $ain highp vec2 position;

         void main(void) {
            gl_Position = vec4(position.x, position.y, 0, 1);
         }"
    val fragment =
        "$fragmentHeader

         void main(void) {
            $fragColor = vec4(1, 1, 1, 0.5);
         }"

    def program = ShaderProgram("Rain", vertex, fragment)
}

class RainShader extends Shader<Object>(RainShaderText().program) {
    static val instance = RainShader()
    val positionSlot = attributeFor("position")

    def loadAttributes(vbDesc : VertexBufferDesc<_>) {
        positionSlot.setFromBufferWith(vbDesc.stride, valuesCount = 2, GL_FLOAT, shift = vbDesc.position)
    }
    def loadUniforms(param : Object) {
    }
}