package com.antonzherdev.trains


import com.antonzherdev.eg._

class RailroadView(railroad : Railroad) {
    private val railView = RailView()
    private val switchView = SwitchView()
    private val lightView = LightView()
    private val damageView = DamageView()
    private val railroadSurface = FullScreenSurface(depth = true, multisampling = true)
    private val backgroundView = BackgroundView(railroad.map)

    private var changed = true
    def init {
        railroad.add(changeListener = changed = true)
    }

    def drawBackground {
        railroadSurface.maybe(changed) {
            egClear
            backgroundView.draw
            railroad.rails.for(railView.draw(_))
            changed = false
        }
        railroadSurface.draw
        railroad.switches.for(switchView.draw(_))
        railroad.builder.rail.for(railView.draw(_))
        railroad.damagesPoints.for(damageView.draw(_))
    }

    def drawForeground {
        railroad.lights.for(lightView.draw(_))
    }
}

class RailView {
    static val railMaterial = StandardMaterial( vec4(0.45, 0.47, 0.55, 1), vec4(0.5, 0.5, 0.5, 1), 1)
    val railModel = MeshModel([
        (Models.railGravel, textureFor("Gravel.png")),
        (Models.railTies, vec4(0.55, 0.45, 0.25, 1)),
        (Models.rails, railMaterial)
    ])
    val railTurnModel = MeshModel([
        (Models.railTurnGravel, textureFor("Gravel.png")),
        (Models.railTurnTies, vec4(0.55, 0.45, 0.25, 1)),
        (Models.railsTurn, railMaterial)
    ])
    def draw(rail : Rail) {
        matrix(
            _.modify(w = w -> w.translate(rail.tile.x, rail.tile.y, 0.001))
             .modify(m = m ->
                if(rail.form == RailForm.bottomTop || rail.form == RailForm.leftRight) {
                    if(rail.form == RailForm.leftRight) m.rotate(90, 0, 1, 0)
                    else m
                } else {
                    if(rail.form == RailForm.topRight) m.rotate(270, 0, 1, 0)
                    else if(rail.form == RailForm.bottomRight) m.rotate(180, 0, 1, 0)
                    else if(rail.form == RailForm.leftBottom) m.rotate(90, 0, 1, 0)
                    else m
                })
        ) {
            if(rail.form == RailForm.bottomTop || rail.form == RailForm.leftRight) {
                railModel.draw
            } else {
                railTurnModel.draw
            }
        }
    }
}

class SwitchView {
    val switchStraightModel = MeshModel([
        (Models.switchStraight, RailView.railMaterial)
    ])
    val switchTurnModel = MeshModel([
        (Models.switchTurn, RailView.railMaterial)
    ])

    def draw(theSwitch : Switch) {
        val connector = theSwitch.connector
        val rail = theSwitch.activeRail
        val form = rail.form
        matrix(
            _.modify(w = w -> w.translate(theSwitch.tile.x, theSwitch.tile.y, 0.03))
             .modify(m = m -> {
                val m2 = m.rotate(connector.angle, 0, 1, 0).translate(-0.5, 0, 0)
                if(form.start.x + form.end.x != 0) {
                    val otherConnector = if(form.start == connector) form.end else form.start
                    val x = connector.x
                    val y = connector.y
                    val ox = otherConnector.x
                    val oy = otherConnector.y
                    if(x == -1 && oy == -1 || y == 1 && ox == -1 || y == -1 && ox == 1 || x == 1 && oy == 1) {
                        m2.scale(1, 1, -1)
                    } else m2
                } else m2
             })
        ) {
            if(form.start.x + form.end.x == 0) {
                switchStraightModel.draw
            } else {
                switchTurnModel.draw
            }
        }
    }
}

class LightView {
    val greenMaterial = StandardMaterial( vec4(0.07568, 0.61424, 0.07568, 1), vec4(0.633, 0.727811, 0.633, 1), 1)
    val redMaterial = StandardMaterial(vec4(0.61424, 0.04136, 0.04136, 1), vec4(0.727811, 0.626959, 0.626959, 1), 1)
    val inactiveMaterial = StandardMaterial(vec4(0.3, 0.3, 0.3, 1), vec4(1, 1, 1, 1), 1)
    val bodyMaterial = StandardMaterial(vec4(0.1, 0.1, 0.1, 1), vec4(0.1, 0.1, 0.1, 1), 1)
    val greenGlowMaterial = ColorSource(vec4(0, 1, 0, 0.8), textureFor("LightGlow.png"))
    val redGlowMaterial = ColorSource(vec4(1, 0, 0, 0.8), textureFor("LightGlow.png"))

    def draw(light : RailLight) {
        matrix(
            _.modify(w = w -> w.translate(light.tile.x, light.tile.y, 0))
             .modify(m = m -> m.rotate(90 + light.connector.angle, 0, 1, 0).translate(0.2, 0, -0.45))
        ) {
            bodyMaterial.draw(Models.light)
            if(light.isGreen) {
                greenMaterial.draw(Models.lightGreen)
                inactiveMaterial.draw(Models.lightRed)
                glDisable(GL_CULL_FACE)
                BlendFunction.standard {
                    greenGlowMaterial.draw(Models.lightGreenGlow)
                }
                glEnable(GL_CULL_FACE)
            } else {
                inactiveMaterial.draw(Models.lightGreen)
                redMaterial.draw(Models.lightRed)
                glDisable(GL_CULL_FACE)
                BlendFunction.standard {
                    redGlowMaterial.draw(Models.lightRedGlow)
                }
                glEnable(GL_CULL_FACE)
            }
        }
    }
}

class DamageView {
    val model = MeshModel([(Models.damage, vec4(1, 0, 0, 1))])
    def draw(point : RailPoint) {
        matrix(
            _.modify(w = w -> w.translate(point.point.x, point.point.y, 0.0))
        ) {
            model.draw
        }
    }
}

class BackgroundView(map : MapSso) {
    val mapView = MapSsoView(map)
    val material = StandardMaterial(textureFor("Grass.png"))

    def draw {
        mapView.drawPlaneWith(material)
//        mapView.drawLayout
    }
}