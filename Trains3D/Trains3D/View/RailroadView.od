package com.antonzherdev.trains


import com.antonzherdev.eg._

class RailroadView(railroad : Railroad) extends InputProcessor {
    private val railView : RailView
    private val switchView = SwitchView()
    private val lightView = LightView(railroad)
    private val damageView = DamageView()
    private val railroadSurface = ViewportSurface(depth = true, multisampling = true)
    private val backgroundView : BackgroundView
    private val undoView = UndoView(railroad.builder)
    val shadowVao : VertexArray<_>?

    var _changed = true
    def init {
        weak val weakSelf = self
        railroad.add(changeListener = weakSelf._changed = true)
        context.considerShadows = false
        backgroundView = BackgroundView(railroad.map)
        railView = RailView()
        val shadowParam = ShadowDrawParam([0.3])
        shadowVao =
            if(egPlatform.shadows)  backgroundView.mapView.plane.vao(ShadowDrawShaderSystem.instance, shadowParam, shadow = false)
            else nil
        context.considerShadows = true
    }

    def drawBackground {
        egPushGroupMarker("Railroad background")
        if(context.renderTarget.isShadow) {
            lightView.drawShadow
        } else {
            railroadSurface.draw
            if(egPlatform.shadows) {
                BlendFunction.standard{ context.cullFace.disabled{ context.depthTest.disabled {
                shadowVao.get.draw
                }}}
            }
            railroad.switches.for(switchView.draw(_))
            railroad.builder.rail.for(railView.draw(_))
            railroad.builder.buildingRails.for(railView.draw(_))
            lightView.drawBodies
        }

        egPopGroupMarker
    }

    def drawForeground {
        egPushGroupMarker("Railroad foreground")
        BlendFunction.standard{
            undoView.draw
            lightView.drawGlows
            railroad.damagesPoints.for(damageView.draw(_))
        }
        egPopGroupMarker
    }

    def prepare {
        railroadSurface.maybe(_changed) {
            context.clearColor(vec4(0, 0, 0, 0))
            glClear(GL_COLOR_BUFFER_BIT + GL_DEPTH_BUFFER_BIT)
            context.considerShadows = false
            backgroundView.draw
            railroad.rails.for(railView.draw(_))
            context.considerShadows = true
            _changed = false
        }
    }

    def reshape {
        undoView.reshape
    }

    def process(event : Event) : bool = undoView.process(event)
}

class RailView {
    val railMaterial = StandardMaterial(vec4(0.5, 0.5, 0.6, 1), vec4(0.5, 0.5, 0.5, 1), 0.3)
    val gravel = textureFor("Gravel.png", GL_LINEAR, GL_LINEAR_MIPMAP_NEAREST)
    val railModel = MeshModel([
        (Models.railGravel, gravel),
        (Models.railTies, vec4(0.55, 0.45, 0.25, 1)),
        (Models.rails, railMaterial)
    ])
    val railTurnModel = MeshModel([
        (Models.railTurnGravel, gravel),
        (Models.railTurnTies, vec4(0.55, 0.45, 0.25, 1)),
        (Models.railsTurn, railMaterial)
    ])
    def draw(railBuilding : RailBuilding) {
        val p = railBuilding.progress
        draw(railBuilding.rail, if(p < 0.5) 1 else 2)
    }

    def draw(rail : Rail) {
        draw(rail, 3)
    }

    private def draw(rail : Rail, count : uint4) {
        matrix(
            _.modify(w = w -> w.translate(rail.tile.x, rail.tile.y, 0.001))
             .modify(m = m ->
                if(rail.form == RailForm.bottomTop || rail.form == RailForm.leftRight) {
                    if(rail.form == RailForm.leftRight) m.rotate(90, 0, 1, 0)
                    else m
                } else {
                    if(rail.form == RailForm.topRight) m.rotate(270, 0, 1, 0)
                    else if(rail.form == RailForm.bottomRight) m.rotate(180, 0, 1, 0)
                    else if(rail.form == RailForm.leftBottom) m.rotate(90, 0, 1, 0)
                    else m
                })
        ) {
            if(rail.form == RailForm.bottomTop || rail.form == RailForm.leftRight) {
                railModel.draw(count)
            } else {
                railTurnModel.draw(count)
            }
        }
    }
}

class UndoView(builder : RailroadBuilder) extends InputProcessor with TapProcessor {
    private var font : Font
    def reshape {
        font = fontWith(name = "lucida_grande", 18)
        val textSize = font.measure(Loc.undo)
        val buttonSize = matrix.p.divBySelf(vec4(textSize * 1.5, 0, 0)).xy
        button.rect = Rect(buttonSize.negate/2, buttonSize)
    }

    private var empty : bool = true
    private val button = Billboard(material = vec4(0.85, 0.9, 0.75, 0.8))
    def draw {
        val rail = builder.railForUndo
        if(rail.isEmpty) {
            empty = true
        } else {
            empty = false
            context.depthTest.disabled {
                button.position = vec3(rail.get.tile, 0)
                button.draw
                font.draw(Loc.undo, color = vec4(0.1, 0.1, 0.1, 1), button.position, TextAlignment(0, 0))
            }
        }
    }

    def process(event : Event) : bool = !empty && event.tap(self)
    def tap(event : Event) : bool = {
        val p = event.locationInViewport
        if(button.contains(p)) {
            builder.undo
            true
        } else false
    }
}

class SwitchView {
    val material = StandardMaterial( vec4(0.2, 0.5, 0.15, 1), vec4(0.5, 1, 0.5, 1), 1)
    val switchStraightModel = MeshModel([
        (Models.switchStraight, material)
    ])
    val switchTurnModel = MeshModel([
        (Models.switchTurn, material)
    ])

    def draw(theSwitch : Switch) {
        val connector = theSwitch.connector
        val rail = theSwitch.activeRail
        val form = rail.form
        var ref = false
        matrix(
            _.modify(w = w -> w.translate(theSwitch.tile.x, theSwitch.tile.y, 0.03))
             .modify(m = m -> {
                val m2 = m.rotate(connector.angle, 0, 1, 0).translate(-0.5, 0, 0)
                if(form.start.x + form.end.x != 0) {
                    val otherConnector = if(form.start == connector) form.end else form.start
                    val x = connector.x
                    val y = connector.y
                    val ox = otherConnector.x
                    val oy = otherConnector.y
                    if(x == -1 && oy == -1 || y == 1 && ox == -1 || y == -1 && ox == 1 || x == 1 && oy == 1) {
                        ref = true
                        m2.scale(1, 1, -1)
                    } else m2
                } else m2
             })
        ) {
            if(ref) glCullFace(GL_BACK)
            if(form.start.x + form.end.x == 0) {
                switchStraightModel.draw
            } else {
                switchTurnModel.draw
            }
            if(ref) glCullFace(GL_FRONT)
        }
    }
}

class LightView(railroad : Railroad) {
    val texture = textureFor("Light.png", GL_LINEAR, GL_LINEAR_MIPMAP_NEAREST)
    val redBodyVao = Models.light.vao(StandardMaterial(TextureRegion(texture, Rect(0.5, 0, 1, 1))), false)
    val greenBodyVao = Models.light.vao(StandardMaterial(texture), false)
    val shadowBodyVao = Models.light.vaoShadow
    val greenGlowVao = Models.lightGreenGlow.vao(StandardMaterial(ColorSource(vec4(0, 1, 0, 0.8), textureFor("LightGlow.png"))), false)
    val redGlowVao = Models.lightRedGlow.vao(StandardMaterial(ColorSource(vec4(1, 0, 0, 0.8), textureFor("LightGlow.png"))), false)

    var _changed = true
    def init {
        weak val weakSelf = self
        railroad.add(changeListener = weakSelf._changed = true)
    }

    private var _matrixArr : [(MatrixModel, RailLight)] = []
    private var _matrixArrShadow : [(MatrixModel, RailLight)] = []
    private def calculateMatrixArr : [(MatrixModel, RailLight)] = railroad.lights.chain.map{light ->
        (matrix.value
            .modify(w = w -> w.translate(light.tile.x, light.tile.y, 0))
            .modify(m = m -> m.rotate(90 + light.connector.angle, 0, 1, 0).translate(0.2, 0, -0.45))
        , light)
    }.toArray

    def drawBodies {
        if(_changed) _matrixArr = calculateMatrixArr
        matrix.push
        _matrixArr.for { p ->
            matrix.value = p.a
            (if(p.b.isGreen)greenBodyVao else redBodyVao).draw
        }
        matrix.pop
    }

    def drawShadow {
        if(_changed) _matrixArrShadow = calculateMatrixArr
        matrix.push
        _matrixArrShadow.for { p ->
            matrix.value = p.a
            shadowBodyVao.draw
        }
        matrix.pop
    }

    def drawGlows {
        if(!context.renderTarget.is<ShadowRenderTarget>) {
            matrix.push
            context.cullFace.disabled{
                _matrixArr.for { p ->
                    matrix.value = p.a
                    if(p.b.isGreen) greenGlowVao.draw
                    else redGlowVao.draw
                }
            }
            matrix.pop
        }
    }
}

class DamageView {
    val model = MeshModel([(Models.damage, ColorSource(vec4(1, 0, 0, 0.6)))])
    def draw(point : RailPoint) {
        matrix(
            _.modify(w = w -> w.translate(point.point.x, point.point.y, 0.0))
        ) {
            model.draw
        }
    }
}

class BackgroundView(map : MapSso) {
    val mapView = MapSsoView(map, StandardMaterial(textureFor("Grass.png")))
    def draw {
        mapView.draw
//        mapView.drawLayout
    }
}