package com.antonzherdev.trains


import com.antonzherdev.eg._

class RailroadView(weak levelView : LevelView, level : Level) extends InputProcessor {
    val railroad = level.railroad
    private val railView : RailView
    private val switchView = SwitchView()
    private val lightView = LightView(levelView, level.railroad)
    private val damageView = DamageView(level.railroad)
//    private val railroadSurface = ViewportSurface.toTexture(depth = true, multisampling = true)
    private val iOS6 = egPlatform.isIOSLess("7")
    private val railroadSurface = ViewportSurface.toTexture(depth = true, multisampling = GameDirector.instance.railroadAA)
//    private val railroadSurface = ViewportSurface.toTexture(depth = true, multisampling = false)

    private val backgroundView : BackgroundView
    private val undoView = UndoView(level.builder)
    val shadowVao : VertexArray<_>?

    private val _changed = ReactFlag(initial = true, [
        level.railroad.railWasBuilt, level.railroad.railWasRemoved,
        level.builder.changed, levelView.cameraMove.changed, level.railroad.stateWasRestored])
    def init {
        context.considerShadows = false
        backgroundView = BackgroundView(level)
        railView = RailView(railroad)
        val shadowParam = ShadowDrawParam([0.3], railroadSurface)
        shadowVao =
            if(egPlatform.shadows) backgroundView.mapView.plane.vao(ShadowDrawShaderSystem.instance, shadowParam, shadow = false)
            else nil
        context.considerShadows = true
    }

    def drawBackground(rrState : RailroadState) {
        egPushGroupMarker("Railroad background")
        if(context.renderTarget.isShadow) {
            lightView.drawShadow(rrState)
        } else {
            if(egPlatform.shadows) {
                context.cullFace.disabled{ context.depthTest.disabled {
                    shadowVao.get.draw
                }}
            } else {
                context.depthTest.disabled {
                    railroadSurface.draw
                }
            }
            lightView.drawBodies(rrState)
        }

        egPopGroupMarker
    }

    def drawLightGlows(rrState : RailroadState) {
        BlendFunction.standard{
            damageView.draw(rrState)
            lightView.drawGlows
        }
    }

    def drawForeground(rrState : RailroadState) {
        egPushGroupMarker("Railroad foreground")
        BlendFunction.standard{ context.cullFace.disabled {
            rrState.switches.for(switchView.draw(_))
            undoView.draw
            damageView.drawForeground
        }}
        egPopGroupMarker
    }

    def prepare {
        railroadSurface.maybe(_changed.value) {
            _changed.clear
            context.clearColor(vec4(0, 0, 0, 0))
            glClear(GL_COLOR_BUFFER_BIT + GL_DEPTH_BUFFER_BIT)
            context.considerShadows = false
            drawSurface
            context.considerShadows = true
            if(iOS6) glFinish
        }
    }

    def drawSurface {
        level.builder.state.join(level.railroad.state).waitAndOnSuccess(1) {t ->
            val builderState = t.a
            val rrState = t.b
            backgroundView.draw
            val building = builderState.notFixedRailBuilding.map(_.rail)
            val builderIsLocked = builderState.isLocked
            rrState.rails.for{ rail ->
                if(builderIsLocked || !building.contains(rail)) railView.draw(rail)
            }
            if(!builderIsLocked) {
                builderState.notFixedRailBuilding.for{ nf ->
                    if(nf.isConstruction) railView.draw(nf)
                    else railView.draw(nf.rail, count = 2)
                }
            }
            builderState.buildingRails.for(railView.draw(_))
        }
    }

    def recognizers = undoView.recognizers

    def updateWith(delta : float) {
        damageView.updateWith(delta)
    }
}

class RailView(railroad : Railroad) {
    val railMaterial = StandardMaterial(diffuse = vec4(0.5, 0.5, 0.6, 1),
        specularColor = vec4(0.5, 0.5, 0.5, 1), specularSize = 0.3, normalMap = nil)
    val gravel = compressedTextureFor("Gravel")
    val railModel = MeshModel([
        (Models.railGravel, gravel.colorSource),
        (Models.railTies, ColorSource(vec4(0.55, 0.45, 0.25, 1))),
        (Models.rails, railMaterial)
    ])
    val railTurnModel = MeshModel([
        (Models.railTurnGravel, gravel.colorSource),
        (Models.railTurnTies, ColorSource(vec4(0.55, 0.45, 0.25, 1))),
        (Models.railsTurn, railMaterial)
    ])
    def draw(railBuilding : RailBuilding) {
        val p = if(railBuilding.isConstruction) railBuilding.progress else 1.0 - railBuilding.progress
        draw(railBuilding.rail, if(p < 0.5) 1 else 2)
    }

    def draw(rail : Rail) {
        draw(rail, 3)
    }

    def draw(rail : Rail, count : uint4) {
        matrix(
            _.modify(w = w -> w.translate(rail.tile.x, rail.tile.y, 0.001))
             .modify(m = m ->
                if(rail.form == RailForm.bottomTop || rail.form == RailForm.leftRight) {
                    if(rail.form == RailForm.leftRight) m.rotate(90, 0, 1, 0)
                    else m
                } else {
                    if(rail.form == RailForm.topRight) m.rotate(270, 0, 1, 0)
                    else if(rail.form == RailForm.bottomRight) m.rotate(180, 0, 1, 0)
                    else if(rail.form == RailForm.leftBottom) m.rotate(90, 0, 1, 0)
                    else m
                })
        ) {
            if(rail.form == RailForm.bottomTop || rail.form == RailForm.leftRight) {
                railModel.draw(count)
                val t = rail.tile
                if(railroad.map.isPartial(t)) {
                    if(railroad.map.cutStateFor(t).y != 0) {
                        val dt = (if(rail.form == RailForm.leftRight) rail.form.start else rail.form.end).next(t) - t
                        matrix.value.modify(w = w -> w.translate(dt.x, dt.y, 0.001))
                        railModel.draw(count)
                    }
                }
            } else {
                railTurnModel.draw(count)
            }
        }
    }
}

class UndoView(builder : RailroadBuilder) extends InputProcessor {
    private var empty : bool = true
    private val buttonPos = Var<vec3>(vec3(0, 0, 0))
    private val button = Sprite(
        material = scaledTextureFor("Pause", TextureFormat.RGBA4).region(32, 32, 32, 32).colorSource,
        position = buttonPos
    )
    def draw {
        builder.state.waitAndOnSuccess(1){s ->
            val rail = s.railForUndo
            if(rail.isEmpty || s.isBuilding) {
                empty = true
            } else {
                empty = false
                context.depthTest.disabled {
                    buttonPos.value = vec3(rail.get.tile, 0)
                    button.draw
                }
            }
        }
    }

    def recognizers : Recognizers = Recognizer(Tap()) {event ->
        if(empty) return false
        val p = event.locationInViewport
        if(button.containsViewport(p)) {
            builder.undo
            true
        } else false
    }
}

class SwitchView {
    val material = ColorSource(compressedTextureFor("Switches", TextureFilter.mipmapNearest))
    val switchStraightModel = MeshModel([
        (Models.switchStraight, material)
    ])
    val switchTurnModel = MeshModel([
        (Models.switchTurn, material)
    ])

    def draw(theSwitch : SwitchState) {
        val connector = theSwitch.connector
        val rail = theSwitch.activeRail
        val form = rail.form
        var ref = false
        matrix(
            _.modify(w = w -> w.translate(theSwitch.tile.x, theSwitch.tile.y, 0.03))
             .modify(m = m -> {
                val m2 = m.rotate(connector.angle, 0, 1, 0).translate(-0.5, 0, 0)
                if(form.start.x + form.end.x != 0) {
                    val otherConnector = if(form.start == connector) form.end else form.start
                    val x = connector.x
                    val y = connector.y
                    val ox = otherConnector.x
                    val oy = otherConnector.y
                    if(x == -1 && oy == -1 || y == 1 && ox == -1 || y == -1 && ox == 1 || x == 1 && oy == 1) {
                        ref = true
                        m2.scale(1, 1, -1)
                    } else m2
                } else m2
             })
        ) {
            if(form.start.x + form.end.x == 0) {
                switchStraightModel.draw
            } else {
                switchTurnModel.draw
            }
        }
    }
}

class LightView(weak levelView : LevelView, railroad : Railroad) {
    private val _matrixChanged = ReactFlag(true, [railroad.lightWasBuiltOrRemoved, railroad.railWasBuilt, railroad.stateWasRestored, levelView.cameraMove.changed, context.viewSize, railroad.lightWasTurned])
    private val _matrixShadowChanged = ReactFlag(true, [railroad.lightWasBuiltOrRemoved, railroad.railWasBuilt, railroad.stateWasRestored, levelView.cameraMove.changed, context.viewSize])
    private val _lightGlowChanged = ReactFlag()

    private var _matrixArr : [(MatrixModel, RailLightState)] = []
    private def calculateMatrixArr(rrState : RailroadState) : Chain<(MatrixModel, RailLightState)> = rrState.lights.chain.map{light ->
        (matrix.value.copy
            .modify(w = w -> w.translate(light.tile.x, light.tile.y, 0))
            .modify(m = m -> m
                .rotate(90 + light.connector.angle, 0, 1, 0)
                .translate(light.shift)
            )
        , light)
    }

    private val bodies = MeshUnite(Models.light, _.vao(ColorSource(compressedTextureFor("Light", TextureFilter.linear)), false))
    def drawBodies(rrState : RailroadState) {
        _matrixChanged.process {
            _matrixArr = calculateMatrixArr(rrState).toArray
            bodies.write(_matrixArr.count) {writer ->
                _matrixArr.for{ p ->
                    val g = p.b.isGreen
                    writer.write(map = (if(g) _ else _.uvAdd(vec2(0.5, 0))) * p.a.mwcp)
                }
            }
            _lightGlowChanged.set
        }

        bodies.draw
    }

    private val shadows = MeshUnite(Models.light, _.vaoShadow)
    def drawShadow(rrState : RailroadState) {
        _matrixShadowChanged.process {
            shadows.write(calculateMatrixArr(rrState).map(_.a.mwcp).toArray)
        }
        shadows.draw
    }

    private val glows = MeshUnite(Models.lightGreenGlow, Models.lightGlowIndex,
        _.vao(ColorSource(compressedTextureFor(if(egPlatform.isPhone) "LightGlowPhone" else "LightGlow", TextureFilter.mipmapNearest)), false))

    def drawGlows {
        if(!_matrixArr.isEmpty && !context.renderTarget.is<ShadowRenderTarget>) {
            _lightGlowChanged.process {
                glows.write(_matrixArr.count) {writer ->
                    _matrixArr.for{ p ->
                        writer.write(if(p.b.isGreen) Models.lightGreenGlow else Models.lightRedGlow, mat4 = p.a.mwcp)
                    }
                }
            }
            context.cullFace.disabled{
                glows.draw
            }
        }
    }
}

class DamageView(railroad : Railroad) {
    val model = MeshModel([(Models.damage, ColorSource(vec4(1, 0, 0, 0.3)))])
    def draw(point : RailPoint) {
        matrix(
            _.modify(w = w -> w.translate(point.point.x, point.point.y, 0.0))
             .modify(m = m -> m.rotate(angleFor(point), 0, 1, 0))
        ) {
            model.draw
        }
    }

    def draw(rrState : RailroadState) {
        rrState.damages.points.for(draw(_))
    }

    val sporadicAnimations = MutableCounterArray<RailPoint>()

    val spObs = Level.sporadicDamageNotification.observe{level, point ->
        sporadicAnimations.append(LengthCounter(3), point)
    }

    def drawForeground {
        context.depthTest.disabled {
            sporadicAnimations.for{counter ->
                D2D.drawCircle(backColor = vec4(1, 0, 0, 0.5), strokeColor = vec4(1, 0, 0, 0.5),
                    at = vec3(counter.data.point, 0), radius = 0.5 * (1.0 - counter.time.value), relative = vec2(0, 0))
            }
        }
    }

    private def angleFor(point : RailPoint) : float4 = {
        val p = point.straight
        val a = p.add(-0.1)
        val b = p.add(0.1)
        val line = Line2(p0 = a.point, p1 = b.point)
        val angle = line.degreeAngle
        angle + 90
    }

    def updateWith(delta : float) {
        sporadicAnimations.updateWith(delta)
    }
}

class BackgroundView(level : Level) {
    val mapView = MapSsoView(level.map, ColorSource(compressedTextureFor(level.rules.theme.background, TextureFilter.nearest)))
    def draw {
        context.depthTest.disabled{
            mapView.draw
        }
//        mapView.drawLayout
    }
}