package com.antonzherdev.trains


class LevelChooseMenu extends SceneView {
    static def scene = Scene(LevelChooseMenu())
    val name = "Level Choose manu"
    val maxLevel = GameDirector.instance.maxAvailableLevel
    def camera = Camera2D(vec2(4, 4))
    private val buttons : [Button] = 0.to(3).chain.flatMap{ y ->
        0.to(3).chain.map{ x ->
            val level = (3 - y)*4 + x + 1
            Button(Rect(x, y, 1, 1), drawButton(x, y, level), {
                GameDirector.instance.set(level = level)
                Director.current.resume
            })}
    }.toArray

    private var fontRes : Font
    private var fontBottom : Font
    def reshapeWith(viewport : Rect) {
        fontRes = fontWith(name = "lucida_grande", size = if(egInterfaceIdiom == InterfaceIdiom.phone) 14 else 16)
        fontBottom = fontWith(name = "lucida_grande", size = if(egInterfaceIdiom == InterfaceIdiom.phone) 12 else 14)
    }

    private var _scores = MutableHashMap<uint, LocalPlayerScore>()
    def start {
        1.to(16).for { level ->
            GameDirector.instance.localPlayerScore(level) { score ->
                if(score.isDefined) {
                    _scores.set(level, score.get)
                    Director.current.redraw
                }
            }
        }
    }


    private static val rankProgress = Progress.progress(vec4(232, 255, 208, 255)/255, vec4(255, 249, 217, 255)/255)
    static def rankColor(score : LocalPlayerScore) : vec4 = rankProgress(score.percent)

    private val textColor = vec4(0.1, 0.1, 0.1, 1)
    private def drawButton(x : int, y : int, level : int) = {
        val ph = egInterfaceIdiom.isPhone
        rect : Rect -> {
            val dis = level > maxLevel
            val score : LocalPlayerScore? = _scores.opt(level)

            val color = if(score.isDefined) rankColor(score.get) else vec4(0.95, 0.95, 0.95, 1.0)
            D2D.drawSprite(color, vec3(x, y + 0.8, 0), Rect(0, 0, 1, 0.2))
            if(!dis) {
                D2D.drawSprite(color, vec3(x, y, 0), Rect(0, 0, 1, if(ph) 0.34 else 0.14))
            }
            BlendFunction.standard {
                fontRes.draw(Loc.level(level), vec3(x + 0.5, y + 0.91, 0), TextAlignment(0, 0), color = textColor)
                if(dis) {
                    D2D.drawSprite(vec4(1, 1, 1, 0.8), vec3(x, y, 0), Rect(0, 0, 1, 1))
                } else {
                    val ss = if(score.isDefined) score.get.value else GameDirector.instance.bestScore(level)
                    if(ss > 0 || score.isDefined) {
                        fontBottom.draw(Loc.format(ss), vec3(x + 0.02, y + if(ph) 0.25 else 0.07, 0), TextAlignment(-1, 0), color = textColor)
                    }
                    if(score.isDefined) {
                        fontBottom.draw(Loc.top(score.get),
                            vec3(x + if(ph) 0.02 else 0.98, y + 0.11, 0),
                            TextAlignment(if(ph) -1 else 1, 0), color = textColor)
                    }
                }
            }
        }
    }

    def draw {
        context.depthTest.disabled {
            D2D.drawSprite(textureFor("Levels.jpg"), vec3(0, 0, 0), Rect(0, 0, 4, 4).stripQuad, Rect(0, 0, 1, 0.75).upsideDownStripQuad)

            BlendFunction.standard {
                buttons.for(_.draw)
            }

            1.to(3).for{ c ->
                D2D.drawLine(vec4(0.5, 0.5, 0.5, 1), vec2(c, 0), vec2(c, 5))
                D2D.drawLine(vec4(0.5, 0.5, 0.5, 1), vec2(0, c), vec2(5, c))
            }
        }
    }

    def isProcessorActive : bool = true
    def recognizers : Recognizers = Recognizer(Tap()){event -> buttons.exists(_.tap(event))}

    def viewportWith(viewSize : vec2) : Rect = Rect(0, 0, viewSize)
}
