package com.antonzherdev.trains

import com.antonzherdev.eg._
import core.ODMath._

enum TRRailConnector(x : int, y : int, angle : int) {
    left(-1, 0, 0)
    bottom(0, -1, 90)
    top(0, 1, 270)
    right(1, 0, 180)

    static def connectorFor(x : int, y : int) : TRRailConnector =
        if(x == -1 && y == 0) left
        else if(x == 0 && y == -1) bottom
        else if(x == 0 && y == 1) top
        else if(x == 1 && y == 0) right
        else throw "No rail connector"

    def otherSideConnector =
        if(self == left) right
        else if(self == right) left
        else if(self == top) bottom
        else top

    def next(tile : EGVec2I) = EGVec2I(tile.x + x, tile.y + y)
}

enum TRRailForm(start : TRRailConnector, end : TRRailConnector, isTurn : bool, length : float, pointFun : float -> EGVec2) {
    leftBottom(TRRailConnector.left, TRRailConnector.bottom, true, M_PI_4, x -> EGVec2(-0.5 + 0.5*sin(x*2), -0.5 + 0.5*cos(x*2)))
    leftRight(TRRailConnector.left, TRRailConnector.right, false, 1, x -> EGVec2(x - 0.5, 0))
    leftTop(TRRailConnector.left, TRRailConnector.top, true, M_PI_4, x -> EGVec2(-0.5 + 0.5*sin(x*2), 0.5 - 0.5*cos(x*2)))
    bottomTop(TRRailConnector.bottom, TRRailConnector.top, false, 1, x -> EGVec2(0, x - 0.5))
    bottomRight(TRRailConnector.bottom, TRRailConnector.right, true, M_PI_4, x -> EGVec2(0.5 - 0.5*cos(x*2), -0.5 + 0.5*sin(x*2)))
    topRight(TRRailConnector.top, TRRailConnector.right, true, M_PI_4, x -> EGVec2(0.5 - 0.5*cos(x*2), 0.5 - 0.5*sin(x*2)))

    static def formFor(connector1 : TRRailConnector, connector2 : TRRailConnector) : TRRailForm =
        if(connector1.ordinal > connector2.ordinal) formFor(connector2, connector1)
        else if(connector1 == TRRailConnector.left && connector2 == TRRailConnector.right) leftRight
        else if(connector1 == TRRailConnector.left && connector2 == TRRailConnector.bottom) leftBottom
        else if(connector1 == TRRailConnector.left && connector2 == TRRailConnector.top) leftTop
        else if(connector1 == TRRailConnector.bottom && connector2 == TRRailConnector.top) bottomTop
        else if(connector1 == TRRailConnector.bottom && connector2 == TRRailConnector.right) bottomRight
        else if(connector1 == TRRailConnector.top && connector2 == TRRailConnector.right) topRight
        else throw "No form for connectors"

}

class TRRailPoint(tile : EGVec2I, form : TRRailForm, x : float, back : bool) {
    def add(x : float) = TRRailPoint(tile, form, self.x + x, back)

    def startConnector = if(back) form.end else form.start

    def endConnector = if(back) form.start else form.end

    def isValid = x >= 0 && x <= form.length

    def correct : TRRailPointCorrection = {
        val length = self.form.length
        if(self.x > length) TRRailPointCorrection(TRRailPoint(self.tile, self.form, length, self.back), self.x - length)
        else TRRailPointCorrection(self, 0)
    }

    val point : EGVec2 = calculatePoint
    private def calculatePoint : EGVec2 = {
        val x = if(back) form.length - self.x else self.x
        val f = form.pointFun
        val p : EGVec2 = f(x)
        EGVec2(p.x + tile.x, p.y + tile.y)
    }

    def invert = TRRailPoint(tile, form, form.length - x, !back)
    def set(x : float) = TRRailPoint(tile, form, x, back)

    def nextTile : EGVec2I = endConnector.next(tile)
}

class TRRailPointCorrection(point : TRRailPoint, error : float) {
    def addErrorToPoint : TRRailPoint = if(error == 0) point else point.add(error)
}