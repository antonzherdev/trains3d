package com.antonzherdev.eg

class Texture {
    val id : GLuint = egGenTexture

    def bind {
        bind(GL_TEXTURE_2D)
    }

    def bind(target : GLenum) {
        glEnable(target)
        glBindTexture(target, id)
    }

    def dealloc {
        egDeleteTexture(id)
    }

    static def unbind {
        unbind(GL_TEXTURE_2D)
    }

    static def unbind(target : GLenum) {
        glBindTexture(target, 0)
        glDisable(target)
    }

    def apply(draw: () -> void) {
        apply(GL_TEXTURE_2D, draw)
    }

    def apply(target : GLenum, draw: () -> void) {
        bind(target)
        draw()
        unbind(target)
    }

    def saveTo(file : string) {
        egSaveTextureToFile(id, file)
    }
}

class FileTexture(file : string) extends Texture  {
    private var _loaded = false
    private var _size : vec2

    private def load {
        _size = egLoadTextureFromFile(id, Bundle.fileNameFor(file))
        _loaded = true
    }

    def size : vec2 = {
        if(!_loaded) load
        _size
    }
    def bind(target : GLenum) {
        if(!_loaded) load
        glEnable(target)
        glBindTexture(target, id)
    }
}