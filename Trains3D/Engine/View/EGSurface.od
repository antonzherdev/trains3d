package com.antonzherdev.eg

import com.antonzherdev.eg.EGGL._

class EGSurface(width : uint, height : uint) {
    private val frameBuffer = egGenFrameBuffer
    val texture = EGTexture()
    def init : EGSurface = {
        glBindFramebuffer(GL_FRAMEBUFFER, frameBuffer)
        glBindTexture(GL_TEXTURE_2D, texture.id)

        glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_CLAMP_TO_EDGE)
        glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_CLAMP_TO_EDGE)
        glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_NEAREST)
        glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_NEAREST)
        glTexImage2D(GL_TEXTURE_2D, 0, GL_RGBA, width, height, 0, GL_RGBA, GL_UNSIGNED_BYTE, 0)
        glFramebufferTexture2D(GL_FRAMEBUFFER, GL_COLOR_ATTACHMENT0, GL_TEXTURE_2D, texture.id, 0)

        glBindTexture(GL_TEXTURE_2D, 0)
        glBindFramebuffer(GL_FRAMEBUFFER, 0)
        self
    }

    def dealloc {
        egDeleteFrameBuffer(frameBuffer)
    }

    def apply(draw : () -> void) {
        bind
        draw()
        unbind
    }

    def bind {
        glPushAttrib(GL_VIEWPORT_BIT)
        glBindFramebuffer(GL_FRAMEBUFFER, frameBuffer)
        glViewport(0, 0, width, height)
    }

    def unbind {
        glBindFramebuffer(GL_FRAMEBUFFER, 0)
        glPopAttrib
    }


    private lazy val fullScreenVertexBuffer = EGVertexBuffer(vec2.type).set([0, 0, 1, 0, 1, 1, 0, 1])
    private lazy val fullScreenIndexBuffer = EGIndexBuffer().set([0, 1, 2, 2, 3, 0])
    private lazy val shader = EGSurfaceShader()
    def drawFullScreen {
        texture {
            fullScreenVertexBuffer {
                shader {
                    fullScreenIndexBuffer.draw
                }
            }
        }
    }
}

class EGSurfaceShader {
    static val vertex =
        "attribute vec2 position;
         varying vec2 UV;

         void main(void) {
            gl_Position = vec4(2.0*position.x - 1.0, 2.0*position.y - 1.0, 0, 1);
            UV = position;
        }"
    static val fragment =
        "varying vec2 UV;

         uniform sampler2D texture;

         void main(void) {
            gl_FragColor = texture2D(texture, UV);
         }"
    val program = EGShaderProgram(vertex, fragment)
    val positionSlot = program.attributeFor("position")
    def apply(draw : () -> void) {
        program {
            positionSlot.setFromBufferWith(stride = 2*4, valuesCount = 2, GL_FLOAT, shift = 0)
            draw()
        }
    }
}