package com.antonzherdev.eg

class Surface(depth : bool, size : vec2i) {
    def apply(draw : () -> void) {
        bind
        draw()
        unbind
    }

    def bind

    def unbind

    def frameBuffer : GLint
}

class SimpleSurface(depth : bool, size : vec2i) extends Surface(depth, size) {
    val frameBuffer = egGenFrameBuffer
    private val depthRenderBuffer : GLuint = if(depth) egGenRenderBuffer else 0
    val texture = {
        val t = Texture()
        glBindFramebuffer(GL_FRAMEBUFFER, frameBuffer)
        glBindTexture(GL_TEXTURE_2D, t.id)

        glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_S, GL_CLAMP_TO_EDGE)
        glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_WRAP_T, GL_CLAMP_TO_EDGE)
        glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MIN_FILTER, GL_NEAREST)
        glTexParameteri(GL_TEXTURE_2D, GL_TEXTURE_MAG_FILTER, GL_NEAREST)
        glTexImage2D(GL_TEXTURE_2D, 0, GL_RGBA, size.x, size.y, 0, GL_RGBA, GL_UNSIGNED_BYTE, 0)
        glFramebufferTexture(GL_FRAMEBUFFER, GL_COLOR_ATTACHMENT0, t.id, 0)

        if(depth) {
            glBindRenderbuffer(GL_RENDERBUFFER, depthRenderBuffer)
            glRenderbufferStorage(GL_RENDERBUFFER, GL_DEPTH_COMPONENT, size.x, size.y)
            glFramebufferRenderbuffer(GL_FRAMEBUFFER, GL_DEPTH_ATTACHMENT, GL_RENDERBUFFER, depthRenderBuffer)
        }

        glBindTexture(GL_TEXTURE_2D, 0)
        glBindFramebuffer(GL_FRAMEBUFFER, 0)
        t
    }

    def dealloc {
        egDeleteFrameBuffer(frameBuffer)
        if(depth) egDeleteRenderBuffer(depthRenderBuffer)
    }

    def bind {
        glPushAttrib(GL_VIEWPORT_BIT)
        glBindFramebuffer(GL_FRAMEBUFFER, frameBuffer)
        glViewport(0, 0, size.x, size.y)
    }

    def unbind {
        glBindFramebuffer(GL_FRAMEBUFFER, 0)
        glPopAttrib
    }

}

class MultisamplingSurface(depth : bool, size : vec2i) extends Surface(depth, size) {
    private val depthRenderBuffer : GLuint = if(depth) egGenRenderBufferEXT else 0
    private val colorRenderBuffer : GLuint = egGenRenderBufferEXT
    val frameBuffer = {
        val fb = egGenFrameBufferEXT
        glBindFramebufferEXT(GL_FRAMEBUFFER_EXT, fb)
        glBindRenderbufferEXT(GL_RENDERBUFFER_EXT, colorRenderBuffer)
        glRenderbufferStorageMultisampleEXT(GL_RENDERBUFFER_EXT, 4, GL_RGBA8, size.x, size.y)

        glFramebufferRenderbufferEXT(GL_FRAMEBUFFER_EXT, GL_COLOR_ATTACHMENT0_EXT, GL_RENDERBUFFER_EXT, colorRenderBuffer)
        val status = glCheckFramebufferStatusEXT(GL_FRAMEBUFFER_EXT)
        if(status != GL_FRAMEBUFFER_COMPLETE_EXT) throw "Error in frame buffer color attachment: $status"

        if(depth) {
            glBindRenderbufferEXT(GL_RENDERBUFFER_EXT, depthRenderBuffer)
            glRenderbufferStorageMultisampleEXT(GL_RENDERBUFFER_EXT, 4, GL_DEPTH_COMPONENT, size.x, size.y)
            glFramebufferRenderbufferEXT(GL_FRAMEBUFFER_EXT, GL_DEPTH_ATTACHMENT_EXT, GL_RENDERBUFFER_EXT, depthRenderBuffer)
            val status1 = glCheckFramebufferStatusEXT(GL_FRAMEBUFFER_EXT)
            if(status1 != GL_FRAMEBUFFER_COMPLETE_EXT) throw "Error in frame buffer depth attachment: $status1"
        }


        glBindFramebuffer(GL_FRAMEBUFFER_EXT, 0)
        fb
    }

    def dealloc {
        egDeleteFrameBufferEXT(frameBuffer)
        egDeleteFrameBufferEXT(colorRenderBuffer)
        if(depth) egDeleteRenderBufferEXT(depthRenderBuffer)
    }



    def bind {
        glPushAttrib(GL_VIEWPORT_BIT)
        glBindFramebufferEXT(GL_DRAW_FRAMEBUFFER_EXT, frameBuffer)
        glViewport(0, 0, size.x, size.y)
    }

    def unbind {
        glBindFramebufferEXT(GL_DRAW_FRAMEBUFFER_EXT, 0)
        glPopAttrib
    }
}

class PairSurface(depth : bool, size : vec2i) extends  Surface(depth, size) {
    val multisampling = MultisamplingSurface(depth, size)
    val simple = SimpleSurface(false, size)

    def bind {
        multisampling.bind
    }

    def unbind {
        multisampling.unbind

        glBindFramebufferEXT(GL_READ_FRAMEBUFFER_EXT, multisampling.frameBuffer)
        glBindFramebufferEXT(GL_DRAW_FRAMEBUFFER_EXT, simple.frameBuffer)
        val s = size
        glBlitFramebufferEXT(0, 0, s.x, s.y, 0, 0, s.x, s.y, GL_COLOR_BUFFER_BIT, GL_NEAREST)
        glBindFramebufferEXT(GL_READ_FRAMEBUFFER_EXT, 0)
        glBindFramebufferEXT(GL_DRAW_FRAMEBUFFER_EXT, 0)
    }

    def frameBuffer : GLint = simple.frameBuffer

    def texture : Texture = simple.texture
}

class FullScreenSurfaceShaderParam(texture : Texture, z : float4)

class FullScreenSurfaceShader extends Shader<FullScreenSurfaceShaderParam>(ShaderProgram(vertex, fragment)) {
    static val vertex =
        "attribute vec2 position;
         uniform float z;
         varying vec2 UV;

         void main(void) {
            gl_Position = vec4(2.0*position.x - 1.0, 2.0*position.y - 1.0, z, 1);
            UV = position;
        }"
    static val fragment =
        "varying vec2 UV;

         uniform sampler2D texture;

         void main(void) {
            gl_FragColor = texture2D(texture, UV);
         }"
    val positionSlot = program.attributeFor("position")
    val zUniform = program.uniformFor("z")
    def load(vertexBuffer : VertexBuffer<vec2>, param : FullScreenSurfaceShaderParam) {
        param.texture.bind
        positionSlot.setFromBufferWith(vertexBuffer.stride, valuesCount = 2, GL_FLOAT, shift = 0)
        zUniform.set(f4 = param.z)
    }

    def unload(param : FullScreenSurfaceShaderParam) {
        Texture.unbind
    }
}

class FullScreenSurface(depth : bool, multisampling : bool) {
    private var surface : Surface? = nil

    private def maybeRecreateSurface {
        if(needRedraw) {
            surface = if(multisampling) PairSurface(depth, context.viewport.size) else SimpleSurface(depth, context.viewport.size)
        }
    }

    def needRedraw : bool = surface.isEmpty || surface.get.size != context.viewport.size

    def bind {
        maybeRecreateSurface
        surface.get.bind
    }

    def apply(draw : () -> void) {
        bind
        draw()
        unbind
    }

    def maybe(draw : () -> void) {
        maybe(force = false, draw)
    }

    private var redrawCounter = 0 // To prevent slow resizing
    def maybe(force : bool, draw : () -> void) {
        val nr = needRedraw
        if(nr) redrawCounter++
        if(force || (nr && redrawCounter > 10)) {
            apply(draw)
            redrawCounter = 0
        }
    }

    def unbind {
        surface.get.unbind
    }

    private lazy val fullScreenMesh = Mesh(vec2.type,
        [vec2(0, 0), vec2(1, 0), vec2(1, 1), vec2(0, 1)],
        [0, 1, 2, 2, 3, 0])
    private lazy val shader = FullScreenSurfaceShader()

    def drawWith(z : float4) {
        glDisable(GL_CULL_FACE)
        shader.draw(FullScreenSurfaceShaderParam(texture, z), fullScreenMesh)
        glEnable(GL_CULL_FACE)
    }

    def texture : Texture = case(surface.get) {
        i : SimpleSurface -> i.texture
        i : PairSurface -> i.texture
    }

    def draw {
        if(surface.isEmpty) return nil
        if(redrawCounter > 0) {
            glDisable(GL_DEPTH_TEST)
            glDisable(GL_CULL_FACE)
            shader.draw(FullScreenSurfaceShaderParam(texture, 0), fullScreenMesh)
            glEnable(GL_CULL_FACE)
            glEnable(GL_DEPTH_TEST)
        } else {
            glBindFramebuffer(GL_READ_FRAMEBUFFER, surface.get.frameBuffer)
            val s = surface.get.size
            val v = context.viewport
            glBlitFramebuffer(0, 0, s.x, s.y, v.x, v.y, v.x2, v.y2, GL_COLOR_BUFFER_BIT, GL_NEAREST)
            glBindFramebuffer(GL_READ_FRAMEBUFFER, 0)
        }
    }

}