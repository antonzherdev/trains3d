package com.antonzherdev.eg

struct MeshData(uv : vec2, normal : vec3, position : vec3)

class Mesh<T>(vertexBuffer : VertexBuffer<T>, indexBuffer : IndexBuffer<uint4>) {
    static def vec2(vertexData : PArray<vec2>, indexData : PArray<uint4>) : Mesh<vec2> = Mesh<vec2>(
            vertexBuffer = VertexBuffer.vec2.set(vertexData),
            indexBuffer = IndexBuffer().set(indexData))

    static def apply(vertexData : PArray<MeshData>, indexData : PArray<uint4>) : Mesh<MeshData> = Mesh<MeshData>(
            vertexBuffer = VertexBuffer.mesh.set(vertexData),
            indexBuffer = IndexBuffer().set(indexData))

    static def apply<T>(desc : VertexBufferDesc<T>, vertexData : PArray<T>, indexData : PArray<uint4>) : Mesh<T> = Mesh<T>(
            vertexBuffer = VertexBuffer<T>(desc).set(vertexData),
            indexBuffer = IndexBuffer().set(indexData))

//    static def quad<T>(vertexBuffer : VertexBuffer<T>) : Mesh =
//        Mesh<T>(vertexBuffer, IndexBuffer().set([0, 1, 2, 2, 3, 0]))
}

class Buffer<T>(dataType : PType<T>, bufferType : GLenum, handle : GLuint) {
    private var _length : uint = 0
    private var _count : uint = 0
    def length : uint = _length
    def count : uint = _count
    static def apply<T>(dataType : PType<T>, bufferType : GLenum) : Buffer = Buffer<T>(dataType, bufferType, egGenBuffer)

    def dealoc {
        egDeleteBuffer(handle)
    }

    def set(data : PArray<T>) : self = set(data = data, GL_STATIC_DRAW)
    def set(array : VoidRefArray) : self = set(array = array, GL_STATIC_DRAW)

    def set(data : PArray<T>, usage : GLenum) : self = {
        glBindBuffer(bufferType, handle)
        glBufferData(bufferType, data.length, data.bytes, GL_STATIC_DRAW)
        glBindBuffer(bufferType, 0)
        _length = data.length
        _count = data.count
        self
    }

    def set(array : VoidRefArray<T>, usage : GLenum) : self = {
        glBindBuffer(bufferType, handle)
        glBufferData(bufferType, array.length, array.bytes, GL_STATIC_DRAW)
        glBindBuffer(bufferType, 0)
        _length = array.length
        _count = array.length/dataType.size
        self
    }

    def update(start : uint, count : uint, array : VoidRefArray<T>) : self = {
        glBindBuffer(bufferType, handle)
        glBufferSubData(bufferType, start*dataType.size, count*dataType.size, array.bytes)
        glBindBuffer(bufferType, 0)
        self
    }

    def bind {
        glBindBuffer(bufferType, handle)
    }

    def unbind {
        glBindBuffer(bufferType, 0)
    }

    def apply(draw : () -> void) {
        bind
        draw()
        unbind
    }

    def stride : uint4 = dataType.size
}

class VertexBufferDesc<T>(dataType : PType<T>, position : int4, uv : int4, normal : int4, color : int4, model : int4)
{
    def stride : uint4 = dataType.size

    static def Vec2 = VertexBufferDesc<vec2>(vec2.type, -1, 0, -1, -1, 0)
    static def Vec3 = VertexBufferDesc<vec3>(vec3.type, 0, 0, 0, -1, 0)
    static def Vec4 = VertexBufferDesc<vec4>(vec4.type, 0, 0, 0, 0, 0)
    static def mesh = VertexBufferDesc<MeshData>(MeshData.type, position = 5*4, uv = 0, normal = 2*4, color = -1, model = -1)
}

class VertexBuffer<T>(desc : VertexBufferDesc<T>, handle : GLuint) extends Buffer<T>(desc.dataType, GL_ARRAY_BUFFER, handle) {
    static def apply<T>(desc : VertexBufferDesc<T>) : VertexBuffer<T> = VertexBuffer<T>(desc, egGenBuffer)
    static def vec2 = VertexBuffer<vec2>(VertexBufferDesc.Vec2, egGenBuffer)
    static def vec3 = VertexBuffer<vec3>(VertexBufferDesc.Vec3, egGenBuffer)
    static def vec4 = VertexBuffer<vec4>(VertexBufferDesc.Vec4, egGenBuffer)
    static def mesh = VertexBuffer<MeshData>(VertexBufferDesc.mesh, egGenBuffer)
}

class IndexBuffer(handle : GLuint) extends Buffer<uint4>(UInt4.type, GL_ELEMENT_ARRAY_BUFFER, handle) {
    static def apply : IndexBuffer = IndexBuffer(egGenBuffer)

    def draw {
        bind
        glDrawElements(GL_TRIANGLES, count, GL_UNSIGNED_INT, 0)
        unbind
    }

    def drawWith(start : uint, count : uint) {
        bind
        glDrawElements(GL_TRIANGLES, count, GL_UNSIGNED_INT, 4*start)
        unbind
    }

    def drawByQuads {
        bind
        var i = 0
        while(i + 6 <= count) {
            glDrawElements(GL_TRIANGLES, 6, GL_UNSIGNED_INT, 4*i)
            i += 6
        }
        unbind
    }
}

