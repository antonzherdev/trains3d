import CNFile

import EGGL
import EGBuffer

class EGShaderProgram(handle : GLuint) {
    static def loadFromFiles(vertex : string, fragment : string) : EGShaderProgram =
        linkFromStrings(CNBundle.readToString(vertex), CNBundle.readToString(fragment))

    static def linkFromStrings(vertex : string, fragment : string) : EGShaderProgram =  {
        val vertexShader = compileShaderFor(GL_VERTEX_SHADER, vertex)
        val fragmentShader = compileShaderFor(GL_FRAGMENT_SHADER, fragment)
        val program = linkFromShaders(vertexShader, fragmentShader)
        glDeleteShader(vertexShader)
        glDeleteShader(fragmentShader)
        program
    }


    static def linkFromShaders(vertex : GLuint, fragment : GLuint) : EGShaderProgram = {
        val handle = glCreateProgram
        glAttachShader(handle, vertex)
        glAttachShader(handle, fragment)
        glLinkProgram(handle)
        egGetProgramError(handle).for(throw "Error in shader program linking: " + _)
        EGShaderProgram(handle)
    }

    static def compileShaderFor(shaderType : GLenum, source : string) : GLuint = {
        val shader = glCreateShader(shaderType)
        egShaderSource(shader, source)
        glCompileShader(shader)
        egGetShaderError(shader).for(throw  "Error in shader compiling : " + _ + source)
        shader
    }

    def dealoc {
        glDeleteProgram(handle)
    }

    def set {
        glUseProgram(handle)
    }

    def clear {
        glUseProgram(0)
    }

    def draw(f : () -> void) {
        glUseProgram(handle)
        f()
        glUseProgram(0)
    }

    def attributeHandleFor(name : string ) = EGShaderAttribute(egGetAttribLocation(handle, name))
}

class EGShader(program : EGShaderProgram) {
    def draw(f : () -> void) {
        glUseProgram(program.handle)
        load
        f()
        glUseProgram(0)
    }

    def set {
        glUseProgram(program.handle)
        load
    }

    def load

    def clear {
        glUseProgram(0)
    }

    def attributeHandleFor(name : string ) = EGShaderAttribute(program.attributeHandleFor(name))
}

class EGShaderAttribute(handle : GLuint) {
    def setFrom(buffer : EGBuffer, valuesCount : uint, valuesType : GLenum, shift : uint) : uint = {
        egVertexAttribPointer(handle, valuesCount, valuesType, GL_FALSE, buffer.stride, shift)
        if(valuesType == GL_DOUBLE) valuesCount*shift + 8
        else if(valuesType == GL_FLOAT) valuesCount*shift + 4
        else if(valuesType == GL_BYTE) valuesCount*shift + 1
        else if(valuesType == GL_UNSIGNED_BYTE) valuesCount*shift + 1
        else throw "Unknown type"
    }
}