import EGMath

struct EGVec2(x : float4, y : float4) extends ODComparable<EGVec2> {
    static def apply(vec2i : EGVec2I) = EGVec2(vec2i.x, vec2i.y)

    def add(vec2 : EGVec2) = EGVec2(x + vec2.x, y + vec2.y)
    def sub(vec2 : EGVec2) = EGVec2(x - vec2.x, y - vec2.y)
    def negate = EGVec2(-x, -y)
    def angle : float4 = atan2(y, x)
    def dot(vec2 : EGVec2) : float4 = x*vec2.x + y*vec2.y
    def lengthSquare : float4 = dot(self)
    def length = sqrt(lengthSquare)
    def mul(value : float4) = EGVec2(x*value, y*value)
    def div(value : float4) = EGVec2(x/value, y/value)
    def mid(vec2 : EGVec2) = add(vec2).mul(0.5)
    def distanceTo(vec2 : EGVec2) = sub(vec2).length
    def set(length : float4) = mul(length/self.length)
    def normalize = set(length = 1.0)

    def compare(to : EGVec2) : int = {
        val dX = x.compare(to.x)
        if(dX != 0) dX else y.compare(to.y)
    }
}

struct EGVec2I(x : int, y : int) extends ODComparable<EGVec2I> {
    static def apply(vec2 : EGVec2) = EGVec2I(lround(vec2.x), lround(vec2.y))

    def add(vec2i : EGVec2I) = EGVec2I(x + vec2i.x, y + vec2i.y)
    def sub(vec2i : EGVec2I) = EGVec2I(x - vec2i.x, y - vec2i.y)
    def negate = EGVec2I(-x, -y)

    def compare(to : EGVec2I) : int = {
        val dX = x.compare(to.x)
        if(dX != 0) dX else y.compare(to.y)
    }
}

struct EGVec3(x : float4, y : float4, z : float4) {
    static def apply(vec2 : EGVec2, z : float4) = EGVec3(vec2.x, vec2.y, z)
    def add(v : EGVec3) = EGVec3(x + v.x, y + v.y, z + v.z)
    def sqr = mul(length)
    def mul(k : float4) = EGVec3(k*x, k*y, k*z)
    def dot(vec3 : EGVec3) : float = x*vec3.x + y*vec3.y + z*vec3.z
    def lengthSquare : float = x*x + y*y + z*z
    def length = sqrt(lengthSquare)
    def set(length : float) = mul(length/self.length)
    def normalize = set(length = 1.0)
}

struct EGVec4(x : float4, y : float4, z : float4, w : float4) {
    static def apply(vec3 : EGVec3, w : float4) = EGVec4(vec3.x, vec3.y, vec3.z, w)
    def xyz = EGVec3(x, y, z)
    def mul(k : float4) = EGVec4(k*x, k*y, k*z, k*w)
    def lengthSquare : float = x*x + y*y + z*z + w*w
    def length = sqrt(lengthSquare)
    def set(length : float) = mul(length/self.length)
    def normalize = set(length = 1.0)
}
