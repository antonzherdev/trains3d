package com.antonzherdev.eg

class VertexArray<P> {
    def draw(param : P, start : uint, end : uint)
    def draw(param : P)

    def draw
    def sync(f : () -> void)
    def vertexBuffers : [VertexBuffer<_>]
    def index : IndexSource

    lazy val mutableVertexBuffer : MutableVertexBuffer<_>? = vertexBuffers.find(_.is<MutableVertexBuffer>).map(_.cast<MutableVertexBuffer<_>>)
    def vertexWrite(count : uint4, f : VoidRefArray -> void) {
        mutableVertexBuffer.get.write(count, f)
    }
}

class RouteVertexArray<P>(standard : VertexArray<P>, shadow : VertexArray<P>) extends VertexArray<P> {
    def mesh : VertexArray<P> =
        if(context.renderTarget.is<ShadowRenderTarget>) shadow
        else standard

    def draw(param : P) {
        mesh.draw(param)
    }

    def draw(param : P, start : uint, end : uint) {
        mesh.draw(param, start, end)
    }

    def draw {
        mesh.draw
    }
    def sync(f : () -> void) {
        mesh.sync(f)
    }

    def vertexBuffers : [VertexBuffer<_>] = mesh.vertexBuffers
    def index : IndexSource = mesh.index
}


class SimpleVertexArray<P>(handle : uint4, shader : Shader<P>, vertexBuffers : [VertexBuffer<_>], index : IndexSource) extends VertexArray<P> {
    static def apply<P>(shader : Shader<P>, buffers : [VertexBuffer<_>], index : IndexSource) : SimpleVertexArray<P> =
        SimpleVertexArray<P>(egGenVertexArray, shader, buffers, index)
    val isMutable = index.isMutable || vertexBuffers.chain.find(_.isMutable).isDefined

    def bind {
        context.bindVertexArray(handle, vertexBuffers.head.count, isMutable)
    }

    def unbind {
        context.bindDefaultVertexArray
    }

    def dealloc {
        context.deleteVertexArray(handle)
    }

    def count = vertexBuffers.head.count

    def draw(param : P) {
        if(!index.isEmpty) shader.draw(param, self)
    }

    def draw(param : P, start : uint, end : uint) {
        if(!index.isEmpty) shader.draw(param, self, start, end)
    }

    def draw {
        throw "No default material"
    }

    private val fence = Fence("VAO")
    def sync(f : () -> void) {
        fence.sync(f)
    }
}

class MaterialVertexArray<P>(vao : VertexArray<P>, material : P) extends VertexArray<P> {
    def draw {
        vao.draw(material)
    }

    def draw(param : P) {
        vao.draw(param)
    }

    def draw(param : P, start : uint, end : uint) {
        vao.draw(param, start, end)
    }

    def sync(f : () -> void) {
        vao.sync(f)
    }

    def vertexBuffers : [VertexBuffer<_>] = vao.vertexBuffers
    def index : IndexSource = vao.index
}

class VertexArrayRing<P>(ringSize : uint4, creator : () -> VertexArray<P>) {
    private val _ring = MQueue<VertexArray<P>>()

    def next : VertexArray<P> = {
        val buffer = if(_ring.count >= ringSize) _ring.dequeue.get else creator()
        _ring.enqueue(buffer)
        buffer
    }

    def sync(f : VertexArray<P> -> void) {
        val vao = next
        next.sync {
            f(vao)
        }
    }
}