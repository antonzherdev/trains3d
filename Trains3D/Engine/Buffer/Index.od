package com.antonzherdev.eg

trait IndexSource {
    def bind {}
    def draw
    def drawWith(start : uint, count : uint)
}

class IndexBuffer(handle : GLuint, mode : uint4, length : uint, count : uint)
    extends Buffer<uint4>(UInt4.type, GL_ELEMENT_ARRAY_BUFFER, handle) with IndexSource
{
    static def apply(array : VoidRefArray<uint4>) : IndexBuffer = {
        val ib = IndexBuffer(egGenBuffer, GL_TRIANGLES, array.length, array.length/4)
        ib.bind
        glBufferData(GL_ELEMENT_ARRAY_BUFFER, array.length, array.bytes, GL_STATIC_DRAW)
        ib
    }

    static def apply(data : PArray<uint4>) : IndexBuffer = {
        val ib = IndexBuffer(egGenBuffer, GL_TRIANGLES, data.length, data.count)
        ib.bind
        glBufferData(GL_ELEMENT_ARRAY_BUFFER, data.length, data.bytes, GL_STATIC_DRAW)
        ib
    }

    def draw {
        context.draw
        glDrawElements(mode, count, GL_UNSIGNED_INT, 0)
    }

    def drawWith(start : uint, count : uint) {
        context.draw
        glDrawElements(mode, count, GL_UNSIGNED_INT, 4*start)
    }

    def bind {
        context.bindIndexBuffer(handle)
    }
}

class MutableIndexBuffer(handle : GLuint, mode : uint4) extends MutableBuffer<uint4>(UInt4.type, GL_ELEMENT_ARRAY_BUFFER, handle) with IndexSource {
    static def apply : MutableIndexBuffer = MutableIndexBuffer(egGenBuffer, GL_TRIANGLES)
    static def apply(mode : uint4) : MutableIndexBuffer = MutableIndexBuffer(egGenBuffer, mode)

    def draw {
        context.draw
        glDrawElements(mode, count, GL_UNSIGNED_INT, 0)
    }

    def drawWith(start : uint, count : uint) {
        context.draw
        glDrawElements(mode, count, GL_UNSIGNED_INT, 4*start)
    }

    def bind {
        context.bindIndexBuffer(handle)
    }
}

class EmptyIndexSource(mode : uint4) extends IndexSource {
    static val triangleStrip = EmptyIndexSource(GL_TRIANGLE_STRIP)
    static val triangles = EmptyIndexSource(GL_TRIANGLES)
    static val lines = EmptyIndexSource(GL_LINES)

    def draw {
        context.draw
        glDrawArrays(mode, 0, context.vertexBuffer.count)
    }

    def drawWith(start : uint, count : uint) {
        context.draw
        glDrawArrays(mode, start, count)
    }
}

class ArrayIndexSource(array : PArray<uint4>, mode : uint4) extends IndexSource {
    def draw {
        context.bindIndexBuffer(0)
        glDrawElements(mode, array.count, GL_UNSIGNED_INT, array.bytes)
    }

    def drawWith(start : uint, count : uint) {
        context.bindIndexBuffer(0)
        glDrawElements(mode, count, GL_UNSIGNED_INT, array.bytes + 4*start)
    }
}

class VoidRefArrayIndexSource(array : VoidRefArray, mode : uint4) extends IndexSource {
    def bind {
        context.bindIndexBuffer(0)
    }

    def draw {
        context.draw
        glDrawElements(mode, array.length/4, GL_UNSIGNED_INT, array.bytes)
    }

    def drawWith(start : uint, count : uint) {
        context.draw
        glDrawElements(mode, count, GL_UNSIGNED_INT, array.bytes + 4*start)
    }
}

class IndexSourceGap(source : IndexSource, start : uint4, count : uint4) extends IndexSource {
    def bind {
        source.bind
    }

    def draw {
        source.drawWith(start, count)
    }

    def drawWith(start : uint, count : uint) {
        source.drawWith(self.start + start, count)
    }
}
