package com.antonzherdev.eg

class Buffer<T>(dataType : PType<T>, bufferType : uint4, handle : GLuint) {
    def length : uint
    def count : uint
    static def apply<T>(dataType : PType<T>, bufferType : uint4) : Buffer = Buffer<T>(dataType, bufferType, egGenBuffer)

    def dealoc {
        egDeleteBuffer(handle)
    }

    def bind {
        glBindBuffer(bufferType, handle)
    }

    def stride : uint4 = dataType.size
}


class MutableBuffer<T>(dataType : PType<T>, bufferType : uint4, handle : GLuint) extends Buffer<T>(dataType, bufferType, handle) {
    private var _length : uint = 0
    private var _count : uint = 0
    def length : uint = _length
    def count : uint = _count

    def set(data : PArray<T>) : self = {
        bind
        glBufferData(bufferType, data.length, data.bytes, GL_DYNAMIC_DRAW)
        _length = data.length
        _count = data.count
        self
    }

    def set(array : VoidRefArray<T>) : self = {
        bind
        glBufferData(bufferType, array.length, array.bytes, GL_DYNAMIC_DRAW)
        _length = array.length
        _count = array.length/dataType.size
        self
    }

    def set(array : VoidRefArray<T>, count : uint4) : self = {
        bind
        _length = count * dataType.size
        glBufferData(bufferType, _length, array.bytes, GL_DYNAMIC_DRAW)
        _count = count
        self
    }

    def update(start : uint, count : uint, array : VoidRefArray<T>) : self = {
        bind
        glBufferSubData(bufferType, start*dataType.size, count*dataType.size, array.bytes)
        self
    }

}