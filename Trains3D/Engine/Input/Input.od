package com.antonzherdev.eg

trait InputProcessor {
    def process(event : Event) : bool
}

trait MouseProcessor {
    def mouseDown(event : Event) : bool = false
    def mouseDrag(event : Event)  : bool = false
    def mouseUp(event : Event) : bool = false
}

trait TouchProcessor {
    def touchBegan(event : Event) : bool = false
    def touchMoved(event : Event) : bool = false
    def touchEnded(event : Event) : bool = false
    def touchCanceled(event : Event) : bool = false
}

class EventCamera(inverseMatrix : mat4, viewport : rect)

class Event(viewSize : vec2, camera : EventCamera?) {
    def set(camera : EventCamera?) : Event

    def locationInView : vec2
    def location = locationFor(0)
    def locationFor(depth : float) : vec2 =
        if(camera.isEmpty) locationInView
        else segment.r(plane = plane(vec3(0, 0, depth), vec3(0, 0, 1))).xy

    lazy val segment : line3 =
        if(camera.isEmpty) line3(vec3(locationInView, 0), vec3(0, 0, 1000))
        else {
            val viewport = camera.get.viewport

            val loc = (locationInView - viewport.origin)/viewport.size*2 - vec2(1, 1)
            val mat4 = camera.get.inverseMatrix
            val p0 = mat4.mul(vec4 = vec4(loc.x, loc.y, -1, 1))
            val p1 = mat4.mul(vec4 = vec4(loc.x, loc.y, 1, 1))
            line3(p0.xyz, p1.xyz - p0.xyz)
        }

    def isLeftMouseDown : bool
    def isLeftMouseDrag : bool
    def isLeftMouseUp : bool
    def leftMouse(processor : MouseProcessor) : bool =
        if(isLeftMouseDown) processor.mouseDown(self)
        else if(isLeftMouseDrag) processor.mouseDrag(self)
        else if(isLeftMouseUp) processor.mouseUp(self)
        else false


    def isTouchBegan : bool
    def isTouchMoved : bool
    def isTouchEnded : bool
    def isTouchCanceled : bool
    def touch(processor : TouchProcessor) : bool =
        if(isTouchBegan) processor.touchBegan(self)
        else if(isTouchMoved) processor.touchMoved(self)
        else if(isTouchEnded) processor.touchEnded(self)
        else if(isTouchCanceled) processor.touchCanceled(self)
        else false
}

class RectIndex<T>(rects : [(rect, T)]) {
    def apply(point : vec2) : T? = rects.find(_.a.contains(point)).map(_.b)
}