package com.antonzherdev.eg

trait InputProcessor {
    def isProcessorActive : bool = !director.isPaused

    def process(event : Event) : bool
}

trait MouseProcessor {
    def mouseDown(event : Event) : bool = false
    def mouseDrag(event : Event)  : bool = false
    def mouseUp(event : Event) : bool = false
}

trait TouchProcessor {
    def touchBegan(event : Event) : bool = false
    def touchMoved(event : Event) : bool = false
    def touchEnded(event : Event) : bool = false
    def touchCanceled(event : Event) : bool = false
}

class EventCamera(matrix : mat4, viewport : Rect) {
    lazy val inverseMatrix = matrix.inverse
}

class Event(viewSize : vec2, camera : EventCamera?) {
    def set(camera : EventCamera?) : Event

    def locationInView : vec2
    def location = locationFor(0)
    def locationFor(depth : float) : vec2 =
        if(camera.isEmpty) locationInView
        else segment.r(plane = Plane(vec3(0, 0, depth), vec3(0, 0, 1))).xy

    lazy val segment : Line3 =
        if(camera.isEmpty) Line3(vec3(locationInView, 0), vec3(0, 0, 1000))
        else {
            val viewport = camera.get.viewport

            val loc = (locationInView - viewport.p0)/viewport.size*2 - vec2(1, 1)
            val mat4 = camera.get.inverseMatrix
            val p0 = mat4.mul(vec4 = vec4(loc.x, loc.y, -1, 1))
            val p1 = mat4.mul(vec4 = vec4(loc.x, loc.y, 1, 1))
            Line3(p0.xyz, p1.xyz - p0.xyz)
        }
    def checkViewport : bool = camera.isEmpty || camera.get.viewport.contains(locationInView)

    def isLeftMouseDown : bool
    def isLeftMouseDrag : bool
    def isLeftMouseUp : bool
    def leftMouse(processor : MouseProcessor) : bool =
        if(!checkViewport) false
        else if(isLeftMouseDown) processor.mouseDown(self)
        else if(isLeftMouseDrag) processor.mouseDrag(self)
        else if(isLeftMouseUp) processor.mouseUp(self)
        else false


    def isTouchBegan : bool
    def isTouchMoved : bool
    def isTouchEnded : bool
    def isTouchCanceled : bool
    def touch(processor : TouchProcessor) : bool =
        if(!checkViewport) false
        else if(isTouchBegan) processor.touchBegan(self)
        else if(isTouchMoved) processor.touchMoved(self)
        else if(isTouchEnded) processor.touchEnded(self)
        else if(isTouchCanceled) processor.touchCanceled(self)
        else false
}
