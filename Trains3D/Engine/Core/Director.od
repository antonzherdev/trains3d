package com.antonzherdev.eg

class Director {
    static private var _current : Director
    static def current : Director = _current

    private var _scene : Scene? = nil
    private var _isStarted = false
    private var _isPaused = false
    private var _lazyScene : (() -> Scene)? = nil
    val time : Time = Time()
    def scene = _scene
    def set(scene : () -> Scene) {
        _lazyScene = scene
        if(_scene.isDefined) {
            _scene.get.stop
            _scene = nil
            clearRecognizers
        }
        if(_isPaused) redraw
    }

    private def maybeNewScene {
        if(_lazyScene.isDefined) {
            context.scale = scale
            val f = _lazyScene.get
            val sc = f()
            _lazyScene = nil
            _scene = sc
            if(_lastViewSize != vec2(0, 0)) sc.reshapeWith(_lastViewSize)
            sc.recognizersTypes.for(register(_))
            sc.start
        }
    }

    def clearRecognizers
    def register(recognizerType : RecognizerType<_>)

    def scale : float
    def lock
    def unlock
    def redraw

    def init {
        _current = self
    }

    static val reshapeNotification = NotificationHandle<Director, vec2>("Reshape")

    private var _lastViewSize : vec2 = vec2(0, 0)
    def viewSize : vec2 = _lastViewSize

    def reshapeWith(size : vec2) {
        if(_lastViewSize != size) {
//            log("reshape($size) {")
            context.viewSize = size
            context.scale = scale
            _lastViewSize = size
            _scene.for(_.reshapeWith(size))
            reshapeNotification.post(sender = self, data = size)
//            log("} reshape")
        }
    }

    def prepare {
        executeDefers
        if(_lastViewSize.x <= 0 || _lastViewSize.y <= 0) return nil
        maybeNewScene
        if(_scene.isEmpty) return nil
        //        log("draw {")
        val sc = _scene.get

        egPushGroupMarker("Prepare")
        _current = self
        context.clear
        context.depthTest.enable
        sc.prepareWith(_lastViewSize)
        egPopGroupMarker
    }

    def draw {
        if(_scene.isEmpty) return nil
        if(_lastViewSize.x <= 0 || _lastViewSize.y <= 0) return nil
//        log("draw {")
        val sc = _scene.get

        egPushGroupMarker("Draw")
        context.clear
        context.depthTest.enable
        context.clearColor(_scene.get.backgroundColor)
        context.set(viewport = Rect(vec2(0, 0), _lastViewSize))
        glClear(GL_COLOR_BUFFER_BIT + GL_DEPTH_BUFFER_BIT)
        sc.drawWith(_lastViewSize)

        if(_stat.isDefined) {
            context.depthTest.disable
            _stat.get.draw
        }
        egPopGroupMarker
//        log("} draw")
    }


    def process(event : Event) {
        if(_scene.isDefined) {
//            weak val sc = _scene.get
            _scene.get.process(event)
        }
    }

    def isStarted = _isStarted
    def start {
        _isStarted = true
        time.start
    }
    def stop {
        _isStarted = false
    }

    def isPaused = _isPaused
    def pause {
        _isPaused = true
        _scene.for(_.pause)
        redraw
    }
    def resignActive {
        _isPaused = true
        _scene.for(_.pause)
    }

    def resume {
        if(_isPaused) {
            _isPaused = false
            time.start
            _scene.for(_.resume)
        }
    }

    private var _timeSpeed = 1.0
    def timeSpeed = _timeSpeed
    def set(timeSpeed : float) {
        if(_timeSpeed != timeSpeed) {
            _timeSpeed = timeSpeed
            SoundDirector.instance.timeSpeed = _timeSpeed
        }
    }

    def tick {
//        log("tick {")
        _current = self
        time.tick
        val dt = time.delta*_timeSpeed
//        log("$dt")
        _scene.for(_.updateWith(dt))
        _stat.for(_.tickWith(time.delta))
//        log("} tick")
    }

    private var _stat : Stat? = nil
    def stat = _stat
    def isDisplayingStats = _stat.isDefined
    def displayStats {
        _stat = Stat()
    }
    def cancelDisplayingStats {_stat = nil}

    private val _defers = ConcurrentQueue<() -> void>()
    def onGLThread(f : () -> void) {
        _defers.enqueue(f)
    }
    private def executeDefers {
        while(true) {
            val f = _defers.dequeue
            if(f.isEmpty) break
            val ff = f.get
            ff()
        }
    }
}