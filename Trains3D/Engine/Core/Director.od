package com.antonzherdev.eg

class Director {
    static private var _current : Director
    static def current : Director = _current

    private var _scene : Scene? = nil
    private var _isStarted = false
    private var _isPaused = false
    val time : Time = Time()
    def scene = _scene
    def set(scene : () -> Scene) {
        lock
        if(_scene.isDefined) _scene.get.stop
        val sc = scene()
        _scene = sc
        sc.reshapeWith(_lastViewSize)
        sc.start
        if(_isPaused) redraw
        unlock
    }

    def scale : float
    def lock
    def unlock
    def redraw

    def init {
        _current = self
    }

    static val reshapeNotification = NotificationHandle<vec2>("Reshape")

    private var _lastViewSize : vec2 = vec2(0, 0)
    def drawWith(size : vec2) {
        if(_lastViewSize != size) {
            context.scale = scale
            _lastViewSize = size
            _scene.for(_.reshapeWith(size))
            Director.reshapeNotification.post(size)
        }

        if(_scene.isEmpty) return nil
        if(size.x <= 0 || size.y <= 0) return nil
        val sc = _scene.get

        _current = self
        context.clear
        context.depthTest.enable

        sc.prepareWith(size)
        context.clearColor(_scene.get.backgroundColor)
        glClear(GL_COLOR_BUFFER_BIT + GL_DEPTH_BUFFER_BIT)
        sc.drawWith(size)

        context.depthTest.disable
        matrix.clear
        context.set(viewport = Rect(vec2(0, 0), size))
        _stat.for(_.draw)
    }


    def process(event : Event) {
        _scene.for(_.process(event))
    }

    def isStarted = _isStarted
    def start {
        _isStarted = true
        time.start
    }
    def stop {
        _isStarted = false
    }

    def isPaused = _isPaused
    def pause {
        _isPaused = true
        _scene.for(_.pause)
        redraw
    }

    def resume {
        if(_isPaused) {
            _isPaused = false
            time.start
            _scene.for(_.resume)
        }
    }

    def tick {
        _current = self
        time.tick
        _scene.for(_.updateWith(time.delta))
        _stat.for(_.tickWith(time.delta))
    }

    private var _stat : Stat? = nil
    def stat = _stat
    def isDisplayingStats = _stat.isDefined
    def displayStats {
        _stat = Stat()
    }
    def cancelDisplayingStats {_stat = nil}
}