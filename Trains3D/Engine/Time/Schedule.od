package com.antonzherdev.eg

class Schedule extends Updatable {
    private var _map = MTreeMap<float, [() -> void]>()
    private var _current = 0.0
    private var _next = -1.0

    def schedule(after : float, weak event : () -> void) {
        _map.modify(key = _current + after, _.getOrElse([]) + event)
        _next = _map.firstKey.get
    }

    def updateWith(delta : float) {
        _current += delta
        while(_next >= 0 && _current > _next) {
            _map.pollFirst.get.b.for {event ->
                event()
            }
            _next = _map.firstKey.getOr(-1.0)
        }
    }

    def time : float = _current

    def isEmpty : bool = _next < 0.0
}

class Counter extends Updatable {
    def isRunning : React<bool>
    def time : React<float>
//    def invTime : float = 1.0 - time
//    def isStopped : bool = !isRunning
    def restart

    def for(f : float -> void) {
        if(isRunning.value) f(time.value)
    }
    def updateWith(delta : float)

    static def apply(length : float) : Counter = LengthCounter(length)
    static def apply(length : float, finish : void -> void) : Counter = Finisher(LengthCounter(length), finish)
    static def apply : Counter = EmptyCounter()

    def on(time : float, event : void -> void) : Counter = EventCounter(self, time, event)
    def onEnd(event : void -> void) : Counter = Finisher(self, event)
}

class EmptyCounter extends Counter {
    static val instance : EmptyCounter = EmptyCounter()

    def isRunning : React<bool> = Val<bool>(false)
    def time : React<float> = Val<float>(0)
    def updateWith(delta : float) {}
    def restart{}
}

class LengthCounter(length : float) extends Counter {
    private var _time = Var<float>(0.0)
    private var _run = Var<bool>(true)
    def time : React<float> = _time
    def isRunning : React<bool> = _run

    def updateWith(delta : float) {
        if(_run.value) {
            var t = _time.value
            t += delta/length
            if(t >= 1.0) {
                _time.value = 1.0
                _run.value = false
            } else {
                _time.value = t
            }
        }
    }
    def restart{
        _time.set(0.0)
        _run.value = true
    }
}

class Finisher(counter : Counter, finish : void -> void) extends Counter {
    def isRunning : React<bool> = counter.isRunning
    def time : React<float> = counter.time
    private val obs = counter.isRunning.observe{r ->
        if(!r) finish()
    }
    def updateWith(delta : float) {
        counter.updateWith(delta)
    }
    def restart {
        counter.restart
    }
}

class EventCounter(counter : Counter, eventTime : float, event : void -> void) extends Counter {
    def isRunning : React<bool> = counter.isRunning
    def time : React<float> = counter.time
    private var executed = false
    private val obs = counter.time.observe{time ->
        if(!executed && counter.time.value > eventTime) {
            event()
            executed = true
        }
    }

    def updateWith(delta : float) {
        counter.updateWith(delta)
    }
    def restart {
        executed = false
        counter.restart
    }
}

class CounterData<T>(counter : Counter, data : T) extends Counter {
    def isRunning : React<bool> = counter.isRunning
    def time : React<float> = counter.time

    def updateWith(delta : float) {
        counter.updateWith(delta)
    }
    def restart {
        counter.restart
    }
}

class MutableCounterArray<T> extends Updatable {
    private var _counters : [CounterData<T>] = []

    def counters : [CounterData<T>] = _counters
    def append(counter : CounterData<T>) {
        _counters += counter
    }

    def append(counter : Counter, data : T) {
        _counters += CounterData<T>(counter, data)
    }

    def updateWith(delta : float) {
        var hasDied = false
        _counters.for{counter ->
            counter.updateWith(delta)
            if(!counter.isRunning.value) hasDied = true
        }
        if(hasDied) _counters = _counters.chain.filter(!_.isRunning.value).toArray
    }

    def for(each : (CounterData<T>) -> void) {
        _counters.for(each)
    }
}