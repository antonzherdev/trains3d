package com.antonzherdev.eg

class Schedule extends Controller {
    private var _map = MutableTreeMap<float, [() -> void]>()
    private var _current = 0.0
    private var _next = -1.0

    def schedule(after : float, event : () -> void) {
        _map.modify(_.getOrElse([]) + event, _current + after)
        _next = _map.firstKey.get
    }

    def updateWith(delta : float) {
        _current += delta
        while(_next > 0 && _current > _next) {
            _map.pollFirst.get.b.for {event ->
                event()
            }
            _next = _map.firstKey.getOr(-1.0)
        }
    }

    def time : float = _current
}

class Counter extends Controller {
    def isRun : bool
    def time : float
    def isStopped : bool = !isRun

    def for(f : float -> void) {
        if(isRun) f(time)
    }
    def updateWith(delta : float)

    static def apply(length : float) : Counter = LengthCounter(length)
    static def apply(length : float, finish : void -> void) : Counter = Finisher(LengthCounter(length), finish)
    static def apply : Counter = EmptyCounter()
}

class EmptyCounter extends Counter {
    def isRun : bool = false
    def time : float = 0
    def updateWith(delta : float) {
    }
}

class LengthCounter(length : float) extends Counter {
    private var _time : float = 0.0
    private var _run : bool = true
    def time : float = _time
    def isRun : bool = _run

    def updateWith(delta : float) {
        if(_run) {
            _time += delta/length
            if(_time >= 1.0) {
                _time = 1.0
                _run = false
            }
        }
    }
}

class Finisher(counter : Counter, finish : void -> void) extends Counter {
    def isRun : bool = counter.isRun
    def time : float = counter.time

    def updateWith(delta : float) {
        if(counter.isRun) {
            counter.updateWith(delta)
            if(counter.isStopped) {
                finish()
            }
        }
    }
}