package com.antonzherdev.eg



class StandardShaderSystem extends ShaderSystem<StandardMaterial> {
    static val instance = StandardShaderSystem()
    def shaderFor(param : StandardMaterial) : Shader<Material> =
        if(context.renderTarget.is<ShadowRenderTarget>)
            if(ShadowShaderSystem.isColorShaderFor(param.diffuse)) StandardShadowShader.instanceForColor
            else StandardShadowShader.instanceForTexture
        else {
            val lights = context.environment.lights
            val directLightsWithShadowsCount = lights.chain.filter(_.is<DirectLight> && _.hasShadows).count
            val directLightsWithoutShadowsCount = lights.chain.filter(_.is<DirectLight> && !_.hasShadows).count
            val texture = param.diffuse.texture
            val t = texture.isDefined
            val region = t && texture.get.is<TextureRegion>
            val key =
                if(egPlatform.shadows && context.considerShadows)
                    StandardShaderKey(directLightsWithShadowsCount, directLightsWithoutShadowsCount, t, region)
                else StandardShaderKey(0, directLightsWithShadowsCount + directLightsWithoutShadowsCount, t, region)

            shaders.objectFor(key, orUpdateWith = key.shader)
        }

    private static val shaders = MutableHashMap<StandardShaderKey, StandardShader>()
}

class StandardShadowShader(shadowShader : ShadowShader) extends Shader<StandardMaterial>(shadowShader.program) {
    static val instanceForColor = StandardShadowShader(ShadowShader.instanceForColor)
    static val instanceForTexture = StandardShadowShader(ShadowShader.instanceForTexture)

    def loadAttributes(vbDesc : VertexBufferDesc<_>) {
        shadowShader.loadAttributes(vbDesc)
    }
    def loadUniforms(param : StandardMaterial) {
        shadowShader.loadUniforms(param.diffuse)
    }
}

class StandardShaderKey(directLightWithShadowsCount : uint, directLightWithoutShadowsCount : uint, texture : bool, region : bool) extends ShaderTextBuilder {
    val directLightCount = directLightWithShadowsCount + directLightWithoutShadowsCount
    def shader : StandardShader = {
        val vertexShader =
            "$vertexHeader
             $ain mediump vec3 normal;

             $ain highp vec3 position;
             uniform mat4 mwcp;
             uniform mat4 mwc;
            $if(region)
             uniform mediump vec2 uvShift;
             uniform mediump vec2 uvScale;
            $endif
             $lightsVertexUniform

            $if(texture)
             $ain mediump vec2 vertexUV;
             $out mediump vec2 UV;
            $endif
             $lightsOut

             void main(void) {
                vec3 normalMWC = normalize((mwc * vec4(normal, 0)).xyz);
                vec3 eyeDirection = normalize(-(mwc * vec4(position, 1)).xyz);
                gl_Position = mwcp * vec4(position, 1);
             $if(texture && region)
                UV = uvScale*vertexUV + uvShift;
             $else
                UV = vertexUV; $when(texture)
             $endif
                $lightsCalculateVaryings
             }"
        val fragmentShader =
            "$fragmentHeader
            $if(texture)
             $in mediump vec2 UV;
             uniform lowp sampler2D diffuseTexture;
            $endif
             uniform lowp vec4 diffuseColor;
             uniform lowp vec4 ambientColor;
             uniform lowp vec4 specularColor;
             uniform lowp float specularSize;
             $lightsIn
             $lightsFragmentUniform

             void main(void) {
                mediump float visibility;$when(directLightWithShadowsCount > 0)
               $if(texture)
                lowp vec4 materialColor = diffuseColor * $texture2D\(diffuseTexture, UV);
               $else
                lowp vec4 materialColor = diffuseColor;
               $endif
                lowp vec4 color = ambientColor * materialColor;
                $lightsDiffuse
                $fragColor = color;
             }"
        StandardShader(self, ShaderProgram(vertexShader, fragmentShader))
    }

    def lightsVertexUniform : string = directLightCount.range.chain.map{i ->
        "uniform mediump vec3 dirLightDirection$i;
        $if(i < directLightWithShadowsCount)
         uniform mat4 dirLightDepthMwcp$i;
        $endif
        "}.toStringWith("\n")
    def lightsIn : string = directLightCount.range.chain.map{i ->
        "$in mediump float dirLightDirectionCos$i;
         $in mediump float dirLightDirectionCosA$i;
        $if(i < directLightWithShadowsCount)
         $in mediump vec3 dirLightShadowCoord$i;
        $endif
        "}.toStringWith("\n")
    def lightsOut : string = directLightCount.range.chain.map{i ->
        "$out mediump float dirLightDirectionCos$i;
         $out mediump float dirLightDirectionCosA$i;
        $if(i < directLightWithShadowsCount)
         $out mediump vec3 dirLightShadowCoord$i;
        $endif
        "}.toStringWith("\n")
    def lightsCalculateVaryings : string = directLightCount.range.chain.map{i ->
        "dirLightDirectionCos$i = max(dot(normalMWC, -dirLightDirection$i), 0.0);
         dirLightDirectionCosA$i = max(dot(eyeDirection, reflect(dirLightDirection$i, normalMWC)), 0.0);
        $if(i < directLightWithShadowsCount)
         dirLightShadowCoord$i = (dirLightDepthMwcp$i * vec4(position, 1)).xyz;
        $endif
        "
    }.toStringWith("\n")


    def lightsFragmentUniform : string = directLightCount.range.chain.map{i ->
        "uniform lowp vec4 dirLightColor$i;
        $if(i < directLightWithShadowsCount)
         uniform mediump sampler2DShadow dirLightShadow$i;
        $endif"}.toStringWith("\n")
    def lightsDiffuse : string =  directLightCount.range.chain.map{i ->
        "
        $if(i < directLightWithShadowsCount)
         visibility = texture(dirLightShadow$i, vec3(dirLightShadowCoord$i.xy, dirLightShadowCoord$i.z - 0.005));
         color += visibility * dirLightDirectionCos$i * (materialColor * dirLightColor$i);
         color += visibility * specularColor * dirLightColor$i * pow(dirLightDirectionCosA$i, 5.0/specularSize);
        $else
         color += dirLightDirectionCos$i * (materialColor * dirLightColor$i);
         color += specularColor * dirLightColor$i * pow(dirLightDirectionCosA$i, 5.0/specularSize);
        $endif
        "
    }.toStringWith("\n")

}

class StandardShader(key : StandardShaderKey, program : ShaderProgram) extends Shader<StandardMaterial>(program) {
    val positionSlot : ShaderAttribute = attributeFor("position")
    val normalSlot : ShaderAttribute? = if(key.directLightCount > 0) attributeFor("normal") else nil
    val uvSlot : ShaderAttribute? = if(key.texture) attributeFor("vertexUV") else nil

    val diffuseTexture : ShaderUniformI4? = if(key.texture) uniformI4("diffuseTexture") else nil
    val uvScale : ShaderUniformVec2? = if(key.region) uniformVec2("uvScale") else nil
    val uvShift : ShaderUniformVec2? = if(key.region) uniformVec2("uvShift") else nil
    val ambientColor = uniformVec4("ambientColor")
    val specularColor : ShaderUniformVec4? = if(key.directLightCount > 0) uniformVec4("specularColor") else nil
    val specularSize : ShaderUniformF4? = if(key.directLightCount > 0) uniformF4("specularSize") else nil
    val diffuseColorUniform = uniformVec4("diffuseColor")
    val mwcpUniform = uniformMat4("mwcp")
    val mwcUniform : ShaderUniformMat4? = if(key.directLightCount > 0) uniformMat4("mwc") else nil

    val directLightDirections : [ShaderUniformVec3] = key.directLightCount.range.chain.map{i -> uniformVec3("dirLightDirection$i")}.toArray
    val directLightColors : [ShaderUniformVec4] = key.directLightCount.range.chain.map{i -> uniformVec4("dirLightColor$i")}.toArray
    val directLightShadows : [ShaderUniformI4] = key.directLightWithShadowsCount.range.chain.map{i -> uniformI4("dirLightShadow$i")}.toArray
    val directLightDepthMwcp : [ShaderUniformMat4] = key.directLightWithShadowsCount.range.chain.map{i -> uniformMat4("dirLightDepthMwcp$i")}.toArray


    def loadAttributes(vbDesc : VertexBufferDesc<_>) {
        positionSlot.setFromBufferWith(vbDesc.stride, valuesCount = 3, GL_FLOAT, vbDesc.position)
        if(key.texture) {
            uvSlot.get.setFromBufferWith(vbDesc.stride, valuesCount = 2, GL_FLOAT, vbDesc.uv)
        }
        if(key.directLightCount > 0) {
            normalSlot.get.setFromBufferWith(vbDesc.stride, valuesCount = 3, GL_FLOAT, vbDesc.normal)
        }
    }

    def loadUniforms(param : StandardMaterial) {
        mwcpUniform(matrix = matrix.value.mwcp)
        if(key.texture) {
            val tex = param.diffuse.texture.get
            context.bindTexture(tex)
            diffuseTexture.get.apply(i4 = 0)
            if(key.region) {
                val r = tex.cast<TextureRegion>.rect
                uvShift.get.apply(vec2 = r.p0)
                uvScale.get.apply(vec2 = r.size)
            }
        }
        diffuseColorUniform(vec4 = param.diffuse.color)
        val env = context.environment
        ambientColor(vec4 = env.ambientColor)
        if(key.directLightCount > 0) {
            specularColor.get.apply(vec4 = param.specularColor)
            specularSize.get.apply(f4 = param.specularSize)
            mwcUniform.get(matrix = context.matrixStack.value.mwc)
            var i : uint = 0
            if(key.directLightWithShadowsCount > 0) {
                env.lights.chain.filter(_.is<DirectLight> && _.hasShadows).for { light ->
                    val dir = matrix.value.wc.mul(vec3 = light.cast<DirectLight>.direction, 0).xyz
                    directLightDirections[i].apply(vec3 = dir.normalize)
                    directLightColors[i].apply(vec4 = light.color)
                    directLightDepthMwcp[i].apply(matrix = light.shadowMap.biasDepthCp * matrix.mw)
                    directLightShadows[i].apply(i4 = i + 1)
                    context.bindTexture(GL_TEXTURE0 + i + 1, GL_TEXTURE_2D, light.shadowMap.texture)
                    i++
                }
            }
            if(key.directLightWithoutShadowsCount > 0) {
                env.lights.chain.filter(_.is<DirectLight> && (!_.hasShadows || !context.considerShadows)).for { light ->
                    val dir = matrix.value.wc.mul(vec3 = light.cast<DirectLight>.direction, 0).xyz
                    directLightDirections[i].apply(vec3 = dir.normalize)
                    directLightColors[i].apply(vec4 = light.color)
                }
            }
        }
    }
}