package com.antonzherdev.eg

import com.antonzherdev.eg.EGGL._

class EGShaderProgram(handle : GLuint) {
    static def loadFromFiles(vertex : string, fragment : string) : EGShaderProgram =
        EGShaderProgram(CNBundle.readToString(vertex), CNBundle.readToString(fragment))

    static def apply(vertex : string, fragment : string) : EGShaderProgram =  {
        val vertexShader = compileShaderFor(GL_VERTEX_SHADER, vertex)
        val fragmentShader = compileShaderFor(GL_FRAGMENT_SHADER, fragment)
        val program = linkFromShaders(vertexShader, fragmentShader)
        glDeleteShader(vertexShader)
        glDeleteShader(fragmentShader)
        program
    }


    static def linkFromShaders(vertex : GLuint, fragment : GLuint) : EGShaderProgram = {
        val handle = glCreateProgram
        glAttachShader(handle, vertex)
        glAttachShader(handle, fragment)
        glLinkProgram(handle)
        egGetProgramError(handle).for(throw "Error in shader program linking: " + _)
        EGShaderProgram(handle)
    }

    static def compileShaderFor(shaderType : GLenum, source : string) : GLuint = {
        val shader = glCreateShader(shaderType)
        egShaderSource(shader, source)
        glCompileShader(shader)
        egGetShaderError(shader).for(throw  "Error in shader compiling : " + _ + source)
        shader
    }

    def dealoc {
        glDeleteProgram(handle)
    }

    def set {
        glUseProgram(handle)
    }

    def clear {
        glUseProgram(0)
    }

    def apply(draw : () -> void) {
        glUseProgram(handle)
        draw()
        glUseProgram(0)
    }

    def attributeFor(name : string ) : EGShaderAttribute = {
        val h = egGetAttribLocation(handle, name)
        if(h < 0) throw "Could not found attribute for name " + name
        val ret = EGShaderAttribute(h)
        ret
    }
    def uniformFor(name : string ) : EGShaderUniform = {
        val h = egGetUniformLocation(handle, name)
        if(h < 0) throw "Could not found attribute for name " + name
        EGShaderUniform(h)
    }
}

class EGShader<M extends EGMaterial, D>(program : EGShaderProgram) {
    def draw(material : M, mesh : EGMesh<D>) {
        draw(material, mesh, 0, mesh.indexBuffer.count)
    }

    def draw(material : M, mesh : EGMesh<D>, start : uint, count : uint) {
        glUseProgram(program.handle)
        mesh.vertexBuffer {
            load(mesh.vertexBuffer, material)
            mesh.indexBuffer.drawWith(start, count)
            unload(material)
        }
        glUseProgram(0)
    }

    def load(vertexBuffer : EGVertexBuffer<D>, material : M)
    def unload(material : M) {}

    def attributeFor(name : string ) = program.attributeFor(name)
    def uniformFor(name : string ) = program.uniformFor(name)
}

class EGShaderAttribute(handle : GLuint) {
    def setFromBufferWith(stride : uint, valuesCount : uint, valuesType : GLenum, shift : uint) {
        glEnableVertexAttribArray(handle)
        egVertexAttribPointer(handle, valuesCount, valuesType, GL_FALSE, stride, shift)
    }
}

class EGShaderUniform(handle : GLuint) {
    def set(matrix : mat4) {
        glUniformMatrix4fv(handle, 1, GL_FALSE, matrix.array)
    }
    def set(color : EGColor) {
        egUniformColor(handle, color)
    }
    def set(vec3 : vec3) {
        egUniformVec3(handle, vec3)
    }
    def set(number : float4) {
        glUniform1f(handle, number)
    }
}

class EGShaderSystem<M extends EGMaterial> {
    def draw(material : M, mesh : EGMesh) {
        val shader = shaderFor(material)
        shader.draw(material, mesh)
    }

    def shaderFor(material : M) : EGShader
}


