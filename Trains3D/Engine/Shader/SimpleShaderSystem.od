package com.antonzherdev.eg

class SimpleShaderSystem extends ShaderSystem<ColorSource> {
    static val instance = SimpleShaderSystem()
    static val colorShader = SimpleColorShader()
    static val textureShader = SimpleTextureShader()

    def shaderFor(param : ColorSource, renderTarget : RenderTarget) : Shader<Material> =
        if(renderTarget.is<ShadowRenderTarget>) ShadowShaderSystem.instance.shaderFor(param)
        else if(param.texture.isEmpty) colorShader else textureShader
}


class SimpleShaderBuilder(texture : bool) extends ShaderTextBuilder {
    def vertex =
       "$vertexHeader
        $ain highp vec3 position;
        uniform mat4 mvp;

       $if(texture)
        $ain mediump vec2 vertexUV;
        $out mediump vec2 UV;
       $endif

        void main(void) {
            gl_Position = mvp * vec4(position, 1);
            UV = vertexUV; $when(texture)
        }"
    val fragment =
       "$fragmentHeader
       $if(texture)
        $in mediump vec2 UV;
        uniform lowp sampler2D texture;
       $endif
        uniform lowp vec4 color;

        void main(void) {
           $if(texture)
            $fragColor = color * $texture2D\(texture, UV);
           $else
            $fragColor = color;
           $endif
        }"

    def program = ShaderProgram(vertex, fragment)
}


class SimpleColorShader extends Shader<ColorSource>(SimpleShaderBuilder(false).program) {
    val positionSlot : ShaderAttribute = attributeFor("position")
    val colorUniform = uniformVec4("color")
    val mvpUniform = uniformMat4("mvp")

    def loadAttributes(vbDesc : VertexBufferDesc<_>) {
        positionSlot.setFromBufferWith(vbDesc.stride, valuesCount = 3, GL_FLOAT, vbDesc.position)
    }
    def loadUniforms(param : ColorSource) {
        mvpUniform(matrix.value.mwcp)
        colorUniform(param.color)
    }
}

class SimpleTextureShader extends Shader<ColorSource>(SimpleShaderBuilder(true).program) {
    val uvSlot : ShaderAttribute = attributeFor("vertexUV")
    val positionSlot : ShaderAttribute = attributeFor("position")
    val mvpUniform = uniformMat4("mvp")
    val colorUniform = uniformVec4("color")

    def loadAttributes(vbDesc : VertexBufferDesc<_>) {
        positionSlot.setFromBufferWith(vbDesc.stride, valuesCount = 3, GL_FLOAT, vbDesc.position)
        uvSlot.setFromBufferWith(vbDesc.stride, valuesCount = 2, GL_FLOAT, vbDesc.uv)
    }
    def loadUniforms(param : ColorSource) {
        mvpUniform(matrix.value.mwcp)
        colorUniform(param.color)
        context.bindTexture(param.texture.get)
    }
}
